import "vox/ast" as ast

// Async normalization (v0):
// - lift nested `await`/`await?` out of expression trees into the surrounding statement list
// - keep evaluation order (left-to-right) and single-evaluation
//
// This runs during async metadata collection so the async CFG builder can
// remain statement-based (split only on statement-level await points).

pub struct AwaitPoint { pub stmt_idx: i32, pub operand: i32, pub is_try: bool }

fn stmt_expr_id(st: ast.Stmt) -> i32 {
  if match st { ast.Stmt.Let(_sp, _m, _n, _ha, _ann, _e) => true, _ => false } {
    return match st { ast.Stmt.Let(_sp, _m, _n, _ha, _ann, e) => e, _ => -1 };
  }
  if match st { ast.Stmt.ExprStmt(_sp, _e) => true, _ => false } {
    return match st { ast.Stmt.ExprStmt(_sp, e) => e, _ => -1 };
  }
  if match st { ast.Stmt.ReturnStmt(_sp, h, _e) => true, _ => false } {
    let has0: bool = match st { ast.Stmt.ReturnStmt(_sp, h, _e) => h, _ => false };
    if has0 { return match st { ast.Stmt.ReturnStmt(_sp, _h, e) => e, _ => -1 }; }
  }
  return -1;
}

pub fn find_await_points(exprs: ast.ExprPool, ss: Vec[ast.Stmt]) -> Vec[AwaitPoint] {
  let mut out: Vec[AwaitPoint] = Vec();
  let mut i: i32 = 0;
  while i < ss.len() {
    let st: ast.Stmt = ss.get(i);
    let eid: i32 = stmt_expr_id(st);
    if eid != -1 {
      let n: ast.ExprNode = ast.expr_pool_get(exprs, eid);
      if match n { ast.ExprNode.Await(_x) => true, _ => false } {
        out.push(AwaitPoint { stmt_idx: i, operand: match n { ast.ExprNode.Await(x) => x, _ => -1 }, is_try: false });
      } else if match n { ast.ExprNode.Try(_x) => true, _ => false } {
        let x: i32 = match n { ast.ExprNode.Try(y) => y, _ => -1 };
        let nx: ast.ExprNode = ast.expr_pool_get(exprs, x);
        if match nx { ast.ExprNode.Await(_y) => true, _ => false } {
          out.push(AwaitPoint { stmt_idx: i, operand: match nx { ast.ExprNode.Await(y) => y, _ => -1 }, is_try: true });
        }
      }
    }
    i = i + 1;
  }
  return out;
}

fn stmt_span(st: ast.Stmt) -> ast.Span {
  return match st {
    ast.Stmt.Let(sp, _m, _n, _ha, _ann, _e) => sp,
    ast.Stmt.Assign(sp, _n, _e) => sp,
    ast.Stmt.AssignField(sp, _r, _f, _e) => sp,
    ast.Stmt.ExprStmt(sp, _e) => sp,
    ast.Stmt.IfStmt(sp, _c, _t, _h, _e) => sp,
    ast.Stmt.WhileStmt(sp, _c, _b) => sp,
    ast.Stmt.TryBegin(sp, _out) => sp,
    ast.Stmt.TryEnd(sp) => sp,
    ast.Stmt.Break(sp) => sp,
    ast.Stmt.Continue(sp) => sp,
    ast.Stmt.ReturnStmt(sp, _h, _e) => sp,
  };
}

fn add_expr(p0: ast.ExprPool, n: ast.ExprNode, sp: ast.Span) -> ast.AddExprResult {
  return ast.expr_pool_add(p0, n, sp);
}

fn add_ident(p0: ast.ExprPool, sp: ast.Span, name: String) -> ast.AddExprResult {
  return add_expr(p0, ast.ExprNode.Ident(name), sp);
}

fn expr_has_await(exprs: ast.ExprPool, eid: i32) -> bool {
  let n: ast.ExprNode = ast.expr_pool_get(exprs, eid);
  if match n { ast.ExprNode.Await(_x) => true, _ => false } { return true; }
  return match n {
    ast.ExprNode.Int(_v) => false,
    ast.ExprNode.Float(_v) => false,
    ast.ExprNode.Bool(_b) => false,
    ast.ExprNode.Str(_s) => false,
    ast.ExprNode.Ident(_s) => false,
    ast.ExprNode.DotIdent(_s) => false,
    ast.ExprNode.Unary(_op, a) => expr_has_await(exprs, a),
    ast.ExprNode.As(a, _tn) => expr_has_await(exprs, a),
    ast.ExprNode.Binary(_op, l, r) => expr_has_await(exprs, l) || expr_has_await(exprs, r),
    ast.ExprNode.Member(recv, _f) => expr_has_await(exprs, recv),
    ast.ExprNode.Call(callee, _targs, args) => {
      if expr_has_await(exprs, callee) { true } else {
        let mut i: i32 = 0;
        while i < args.len() {
          if expr_has_await(exprs, args.get(i)) { return true; }
          i = i + 1;
        }
        false
      }
    },
    ast.ExprNode.TryBlock(bid) => expr_has_await(exprs, bid),
    ast.ExprNode.Try(a) => expr_has_await(exprs, a),
    ast.ExprNode.Await(_a) => true,
    ast.ExprNode.Block(b) => {
      // Block expression: check nested statement list + optional tail.
      if block_has_await(exprs, ast.Block { stmts: b.stmts }) { true } else if b.has_tail { expr_has_await(exprs, b.tail) } else { false }
    },
    ast.ExprNode.StructLit(_t, fields) => {
      let mut i: i32 = 0;
      while i < fields.len() {
        if expr_has_await(exprs, fields.get(i).expr) { return true; }
        i = i + 1;
      }
      false
    },
    ast.ExprNode.If(c, t, e) => expr_has_await(exprs, c) || expr_has_await(exprs, t) || expr_has_await(exprs, e),
    ast.ExprNode.Match(s, arms) => {
      if expr_has_await(exprs, s) { true } else {
        let mut i: i32 = 0;
        while i < arms.len() {
          if expr_has_await(exprs, arms.get(i).expr) { return true; }
          i = i + 1;
        }
        false
      }
    },
    ast.ExprNode.MacroCall(_c, _t, args) => {
      let mut i: i32 = 0;
      while i < args.len() {
        if expr_has_await(exprs, args.get(i)) { return true; }
        i = i + 1;
      }
      false
    },
  };
}

fn stmt_has_await_anywhere(exprs: ast.ExprPool, st: ast.Stmt) -> bool {
  return match st {
    ast.Stmt.Let(_sp, _m, _n, _ha, _ann, init) => expr_has_await(exprs, init),
    ast.Stmt.Assign(_sp, _n, ex) => expr_has_await(exprs, ex),
    ast.Stmt.AssignField(_sp, _r, _f, ex) => expr_has_await(exprs, ex),
    ast.Stmt.ExprStmt(_sp, ex) => expr_has_await(exprs, ex),
    ast.Stmt.IfStmt(_sp, cond, then_b, has_else, else_b) => {
      if expr_has_await(exprs, cond) { true } else if block_has_await(exprs, then_b) { true } else if has_else { block_has_await(exprs, else_b) } else { false }
    },
    ast.Stmt.WhileStmt(_sp, cond, body) => expr_has_await(exprs, cond) || block_has_await(exprs, body),
    ast.Stmt.TryBegin(_sp, _out) => false,
    ast.Stmt.TryEnd(_sp) => false,
    ast.Stmt.Break(_sp) => false,
    ast.Stmt.Continue(_sp) => false,
    ast.Stmt.ReturnStmt(_sp, has_ex, ex) => if has_ex { expr_has_await(exprs, ex) } else { false },
  };
}

fn block_has_await(exprs: ast.ExprPool, b: ast.Block) -> bool {
  let mut i: i32 = 0;
  while i < b.stmts.len() {
    if stmt_has_await_anywhere(exprs, b.stmts.get(i)) { return true; }
    i = i + 1;
  }
  return false;
}

fn tmp_name(n: i32) -> String { return "__await$".concat(n.to_string()); }

fn match_scrut_name(n: i32) -> String { return "__match_scrut$".concat(n.to_string()); }

fn match_out_name(n: i32) -> String { return "__match_out$".concat(n.to_string()); }

fn try_out_name(n: i32) -> String { return "__try_out$".concat(n.to_string()); }

fn add_str_lit(p0: ast.ExprPool, sp: ast.Span, s: String) -> ast.AddExprResult {
  return add_expr(p0, ast.ExprNode.Str(s), sp);
}

fn add_int_lit(p0: ast.ExprPool, sp: ast.Span, s: String) -> ast.AddExprResult {
  return add_expr(p0, ast.ExprNode.Int(s), sp);
}

fn add_call_expr(p0: ast.ExprPool, sp: ast.Span, callee: i32, args: Vec[i32]) -> ast.AddExprResult {
  return add_expr(p0, ast.ExprNode.Call(callee, Vec(), args), sp);
}

fn add_intr2_expr(p0: ast.ExprPool, sp: ast.Span, name: String, a: i32, b: i32) -> ast.AddExprResult {
  let r0: ast.AddExprResult = add_ident(p0, sp, name);
  let mut as0: Vec[i32] = Vec();
  as0.push(a);
  as0.push(b);
  return add_call_expr(r0.pool, sp, r0.id, as0);
}

fn add_intr3_expr(p0: ast.ExprPool, sp: ast.Span, name: String, a: i32, b: i32, c: i32) -> ast.AddExprResult {
  let r0: ast.AddExprResult = add_ident(p0, sp, name);
  let mut as0: Vec[i32] = Vec();
  as0.push(a);
  as0.push(b);
  as0.push(c);
  return add_call_expr(r0.pool, sp, r0.id, as0);
}

fn add_dot_ident(p0: ast.ExprPool, sp: ast.Span, name: String) -> ast.AddExprResult {
  return add_expr(p0, ast.ExprNode.DotIdent(name), sp);
}

fn add_uninit_id(p0: ast.ExprPool, sp: ast.Span, ty: i32) -> ast.AddExprResult {
  let r0: ast.AddExprResult = add_ident(p0, sp, "@uninit_id");
  let r1: ast.AddExprResult = add_int_lit(r0.pool, sp, ty.to_string());
  let mut args: Vec[i32] = Vec();
  args.push(r1.id);
  return add_call_expr(r1.pool, sp, r0.id, args);
}

struct LiftExprResult { exprs: ast.ExprPool, prefix: Vec[ast.Stmt], id: i32, next_tmp: i32, ok: bool, err: String }

struct InferExprTyResult { ok: bool, ty: i32, err: String }

fn lift_ok(exprs: ast.ExprPool, prefix: Vec[ast.Stmt], id: i32, next_tmp: i32) -> LiftExprResult {
  return LiftExprResult { exprs: exprs, prefix: prefix, id: id, next_tmp: next_tmp, ok: true, err: "" };
}

fn lift_err(exprs: ast.ExprPool, err: String, next_tmp: i32) -> LiftExprResult {
  return LiftExprResult { exprs: exprs, prefix: Vec(), id: -1, next_tmp: next_tmp, ok: false, err: err };
}

fn infer_expr_ty(ctx: Ctx, w: World, mod_path: String, imps: Imports, exprs: ast.ExprPool, l0: Locals, eid: i32, sp: ast.Span) -> InferExprTyResult {
  if eid < 0 || eid >= exprs.nodes.len() {
    return InferExprTyResult { ok: false, ty: -1, err: "internal: expr id out of range in infer_expr_ty at ".concat(sp.file).concat(":").concat(sp.line.to_string()) };
  }
  let pr0: PrepAsyncInstResult = prep_async_inst_in_expr(ctx, w, mod_path, imps, exprs, l0, eid);
  if !pr0.ok {
    return InferExprTyResult { ok: false, ty: -1, err: tc_error_to_string(pr0.err) };
  }
  let tr: ExprTyResult = tc_expr(pr0.ctx, w, mod_path, imps, exprs, l0, eid, -1);
  if tr.ok {
    return InferExprTyResult { ok: true, ty: strip_range_only(pr0.ctx, tr.ty), err: "" };
  }
  if tr.err != TcError.None {
    return InferExprTyResult { ok: false, ty: -1, err: tc_error_to_string(tr.err) };
  }
  return InferExprTyResult { ok: false, ty: -1, err: "typecheck failed at ".concat(sp.file).concat(":").concat(sp.line.to_string()) };
}

fn mk_match_cond(exprs0: ast.ExprPool, sp: ast.Span, scrut: i32, pat: ast.Pat) -> LiftExprResult {
  if match pat { ast.Pat.Wild(_sp) => true, ast.Pat.Bind(_sp, _n) => true, _ => false } {
    let r: ast.AddExprResult = add_expr(exprs0, ast.ExprNode.Bool(true), sp);
    return lift_ok(r.pool, Vec(), r.id, 0);
  }
  if match pat { ast.Pat.Bool(_sp, _b) => true, _ => false } {
    let b: bool = match pat { ast.Pat.Bool(_sp, b0) => b0, _ => false };
    let rb: ast.AddExprResult = add_expr(exprs0, ast.ExprNode.Bool(b), sp);
    let r: ast.AddExprResult = add_expr(rb.pool, ast.ExprNode.Binary(ast.BinaryOp.Eq, scrut, rb.id), sp);
    return lift_ok(r.pool, Vec(), r.id, 0);
  }
  if match pat { ast.Pat.Int(_sp, _t) => true, _ => false } {
    let text: String = match pat { ast.Pat.Int(_sp, t0) => t0, _ => "0" };
    let ri: ast.AddExprResult = add_expr(exprs0, ast.ExprNode.Int(text), sp);
    let r: ast.AddExprResult = add_expr(ri.pool, ast.ExprNode.Binary(ast.BinaryOp.Eq, scrut, ri.id), sp);
    return lift_ok(r.pool, Vec(), r.id, 0);
  }
  if match pat { ast.Pat.Str(_sp, _t) => true, _ => false } {
    let text: String = match pat { ast.Pat.Str(_sp, t0) => t0, _ => "" };
    let rs: ast.AddExprResult = add_expr(exprs0, ast.ExprNode.Str(text), sp);
    let r: ast.AddExprResult = add_expr(rs.pool, ast.ExprNode.Binary(ast.BinaryOp.Eq, scrut, rs.id), sp);
    return lift_ok(r.pool, Vec(), r.id, 0);
  }
  if match pat { ast.Pat.EnumVariant(_sp, _parts, _v, _args) => true, _ => false } {
    let vname: String = match pat { ast.Pat.EnumVariant(_sp, _parts, vn, _args) => vn, _ => "" };
    let rv: ast.AddExprResult = add_str_lit(exprs0, sp, vname);
    let r: ast.AddExprResult = add_intr2_expr(rv.pool, sp, "@enum_is", scrut, rv.id);
    return lift_ok(r.pool, Vec(), r.id, 0);
  }
  return lift_err(exprs0, "unsupported match pattern in async await lowering", 0);
}

fn mk_match_binds(exprs0: ast.ExprPool, sp: ast.Span, scrut: i32, pat: ast.Pat) -> LiftExprResult {
  let mut p: ast.ExprPool = exprs0;
  let mut ss: Vec[ast.Stmt] = Vec();

  if match pat { ast.Pat.Bind(_sp, _n) => true, _ => false } {
    let name: String = match pat { ast.Pat.Bind(_sp, n0) => n0, _ => "" };
    ss.push(ast.Stmt.Let(sp, false, name, false, ast.TypeName { parts: Vec(), args: Vec() }, scrut));
    return lift_ok(p, ss, -1, 0);
  }

  if match pat { ast.Pat.EnumVariant(_sp, _parts, _v, _args) => true, _ => false } {
    let vname: String = match pat { ast.Pat.EnumVariant(_sp, _parts, vn, _args) => vn, _ => "" };
    let args: Vec[ast.Pat] = match pat { ast.Pat.EnumVariant(_sp, _parts, _vn, as0) => as0, _ => Vec() };
    let mut i: i32 = 0;
    while i < args.len() {
      let ap: ast.Pat = args.get(i);
      if match ap { ast.Pat.Wild(_sp) => true, _ => false } {
        // no bind
      } else if match ap { ast.Pat.Bind(_sp, _n) => true, _ => false } {
        let name: String = match ap { ast.Pat.Bind(_sp, n0) => n0, _ => "" };
        let rv: ast.AddExprResult = add_str_lit(p, sp, vname);
        p = rv.pool;
        let ri: ast.AddExprResult = add_int_lit(p, sp, i.to_string());
        p = ri.pool;
        let rp: ast.AddExprResult = add_intr3_expr(p, sp, "@enum_payload", scrut, rv.id, ri.id);
        p = rp.pool;
        ss.push(ast.Stmt.Let(sp, false, name, false, ast.TypeName { parts: Vec(), args: Vec() }, rp.id));
      } else {
        return lift_err(p, "unsupported enum payload pattern in async await lowering", 0);
      }
      i = i + 1;
    }
    return lift_ok(p, ss, -1, 0);
  }

  return lift_ok(p, ss, -1, 0);
}

fn mk_panic_call(exprs0: ast.ExprPool, sp: ast.Span, msg: String) -> ast.AddExprResult {
  let r_panic: ast.AddExprResult = add_ident(exprs0, sp, "panic");
  let r_msg: ast.AddExprResult = add_str_lit(r_panic.pool, sp, msg);
  let mut as0: Vec[i32] = Vec();
  as0.push(r_msg.id);
  return add_call_expr(r_msg.pool, sp, r_panic.id, as0);
}

fn lift_expr(ctx: Ctx, w: World, mod_path: String, imps: Imports, l0: Locals, exprs0: ast.ExprPool, eid: i32, tmp0: i32, sp: ast.Span, allow_root_await: bool) -> LiftExprResult {
  if eid < 0 || eid >= exprs0.nodes.len() {
    return lift_err(exprs0, "internal: expr id out of range in lift_expr at ".concat(sp.file).concat(":").concat(sp.line.to_string()), tmp0);
  }
  let n: ast.ExprNode = ast.expr_pool_get(exprs0, eid);

  // Root await stays in place when allowed; otherwise lift into a temp.
  if match n { ast.ExprNode.Await(_x) => true, _ => false } {
    let op: i32 = match n { ast.ExprNode.Await(x) => x, _ => -1 };
    let ro: LiftExprResult = lift_expr(ctx, w, mod_path, imps, l0, exprs0, op, tmp0, sp, false);
    if !ro.ok { return ro; }

    let mut p: ast.ExprPool = ro.exprs;
    let mut pre: Vec[ast.Stmt] = Vec();
    let mut i: i32 = 0;
    while i < ro.prefix.len() {
      pre.push(ro.prefix.get(i));
      i = i + 1;
    }

    let mut await_id: i32 = eid;
    if !(ro.prefix.len() == 0 && ro.id == op) {
      let r_await: ast.AddExprResult = add_expr(p, ast.ExprNode.Await(ro.id), sp);
      p = r_await.pool;
      await_id = r_await.id;
    }

    if allow_root_await {
      return lift_ok(p, pre, await_id, ro.next_tmp);
    }

    // Lift: let __await$N = <await-expr>; use __await$N
    let nm: String = tmp_name(ro.next_tmp);
    let r_id: ast.AddExprResult = add_ident(p, sp, nm);
    p = r_id.pool;
    pre.push(ast.Stmt.Let(sp, false, nm, false, ast.TypeName { parts: Vec(), args: Vec() }, await_id));
    return lift_ok(p, pre, r_id.id, ro.next_tmp + 1);
  }

  // Root try-await stays in place when allowed; otherwise lift into a temp.
  if match n { ast.ExprNode.Try(_x) => true, _ => false } {
    let x: i32 = match n { ast.ExprNode.Try(y) => y, _ => -1 };
    let nx: ast.ExprNode = ast.expr_pool_get(exprs0, x);
    if match nx { ast.ExprNode.Await(_y) => true, _ => false } {
      let op: i32 = match nx { ast.ExprNode.Await(y) => y, _ => -1 };
      let ro: LiftExprResult = lift_expr(ctx, w, mod_path, imps, l0, exprs0, op, tmp0, sp, false);
      if !ro.ok { return ro; }

      let mut p: ast.ExprPool = ro.exprs;
      let mut pre: Vec[ast.Stmt] = Vec();
      let mut i: i32 = 0;
      while i < ro.prefix.len() {
        pre.push(ro.prefix.get(i));
        i = i + 1;
      }

      let mut await_id: i32 = x;
      if !(ro.prefix.len() == 0 && ro.id == op) {
        let r_await: ast.AddExprResult = add_expr(p, ast.ExprNode.Await(ro.id), sp);
        p = r_await.pool;
        await_id = r_await.id;
      }

      let mut try_id: i32 = eid;
      if !(ro.prefix.len() == 0 && await_id == x) {
        let r_try: ast.AddExprResult = add_expr(p, ast.ExprNode.Try(await_id), sp);
        p = r_try.pool;
        try_id = r_try.id;
      }

      if allow_root_await {
        return lift_ok(p, pre, try_id, ro.next_tmp);
      }

      let nm: String = tmp_name(ro.next_tmp);
      let r_id: ast.AddExprResult = add_ident(p, sp, nm);
      p = r_id.pool;
      pre.push(ast.Stmt.Let(sp, false, nm, false, ast.TypeName { parts: Vec(), args: Vec() }, try_id));
      return lift_ok(p, pre, r_id.id, ro.next_tmp + 1);
    }
  }

  // No await: rewrite children and rebuild if needed.
  // Avoid using `match` as an expression returning a struct here, because our
  // current C backend can mis-generate such code.
  if match n { ast.ExprNode.Int(_v) => true, _ => false } { return lift_ok(exprs0, Vec(), eid, tmp0); }
  if match n { ast.ExprNode.Float(_v) => true, _ => false } { return lift_ok(exprs0, Vec(), eid, tmp0); }
  if match n { ast.ExprNode.Bool(_b) => true, _ => false } { return lift_ok(exprs0, Vec(), eid, tmp0); }
  if match n { ast.ExprNode.Str(_s) => true, _ => false } { return lift_ok(exprs0, Vec(), eid, tmp0); }
  if match n { ast.ExprNode.Ident(_s) => true, _ => false } { return lift_ok(exprs0, Vec(), eid, tmp0); }
  if match n { ast.ExprNode.DotIdent(_s) => true, _ => false } { return lift_ok(exprs0, Vec(), eid, tmp0); }

  if match n { ast.ExprNode.Unary(_op, _a) => true, _ => false } {
    let op: ast.UnaryOp = match n { ast.ExprNode.Unary(op0, _a0) => op0, _ => ast.UnaryOp.Pos };
    let a: i32 = match n { ast.ExprNode.Unary(_op0, a0) => a0, _ => -1 };
    let ra: LiftExprResult = lift_expr(ctx, w, mod_path, imps, l0, exprs0, a, tmp0, sp, false);
    if !ra.ok { return ra; }
    if ra.prefix.len() == 0 && ra.id == a { return lift_ok(ra.exprs, Vec(), eid, ra.next_tmp); }
    let r1: ast.AddExprResult = add_expr(ra.exprs, ast.ExprNode.Unary(op, ra.id), sp);
    return lift_ok(r1.pool, ra.prefix, r1.id, ra.next_tmp);
  }

  if match n { ast.ExprNode.As(_a, _tn) => true, _ => false } {
    let a: i32 = match n { ast.ExprNode.As(a0, _tn0) => a0, _ => -1 };
    let tn: ast.TypeName = match n { ast.ExprNode.As(_a0, tn0) => tn0, _ => ast.TypeName { parts: Vec(), args: Vec() } };
    let ra: LiftExprResult = lift_expr(ctx, w, mod_path, imps, l0, exprs0, a, tmp0, sp, false);
    if !ra.ok { return ra; }
    if ra.prefix.len() == 0 && ra.id == a { return lift_ok(ra.exprs, Vec(), eid, ra.next_tmp); }
    let r1: ast.AddExprResult = add_expr(ra.exprs, ast.ExprNode.As(ra.id, tn), sp);
    return lift_ok(r1.pool, ra.prefix, r1.id, ra.next_tmp);
  }

  if match n { ast.ExprNode.Binary(_op, _l, _r) => true, _ => false } {
    let op: ast.BinaryOp = match n { ast.ExprNode.Binary(op0, _l0, _r0) => op0, _ => ast.BinaryOp.Add };
    let l: i32 = match n { ast.ExprNode.Binary(_op0, left0, _r0) => left0, _ => -1 };
    let r: i32 = match n { ast.ExprNode.Binary(_op0, _l0, r0) => r0, _ => -1 };
    let rl: LiftExprResult = lift_expr(ctx, w, mod_path, imps, l0, exprs0, l, tmp0, sp, false);
    if !rl.ok { return rl; }
    let rr: LiftExprResult = lift_expr(ctx, w, mod_path, imps, l0, rl.exprs, r, rl.next_tmp, sp, false);
    if !rr.ok { return rr; }
    let mut pre: Vec[ast.Stmt] = Vec();
    let mut i: i32 = 0;
    while i < rl.prefix.len() { pre.push(rl.prefix.get(i)); i = i + 1; }
    i = 0;
    while i < rr.prefix.len() { pre.push(rr.prefix.get(i)); i = i + 1; }
    if pre.len() == 0 && rl.id == l && rr.id == r { return lift_ok(rr.exprs, Vec(), eid, rr.next_tmp); }
    let r1: ast.AddExprResult = add_expr(rr.exprs, ast.ExprNode.Binary(op, rl.id, rr.id), sp);
    return lift_ok(r1.pool, pre, r1.id, rr.next_tmp);
  }

  if match n { ast.ExprNode.Member(_recv, _f) => true, _ => false } {
    let recv: i32 = match n { ast.ExprNode.Member(r0, _f0) => r0, _ => -1 };
    let f: String = match n { ast.ExprNode.Member(_r0, f0) => f0, _ => "" };
    let rr: LiftExprResult = lift_expr(ctx, w, mod_path, imps, l0, exprs0, recv, tmp0, sp, false);
    if !rr.ok { return rr; }
    if rr.prefix.len() == 0 && rr.id == recv { return lift_ok(rr.exprs, Vec(), eid, rr.next_tmp); }
    let r1: ast.AddExprResult = add_expr(rr.exprs, ast.ExprNode.Member(rr.id, f), sp);
    return lift_ok(r1.pool, rr.prefix, r1.id, rr.next_tmp);
  }

  if match n { ast.ExprNode.Call(_callee, _targs, _args) => true, _ => false } {
    let callee: i32 = match n { ast.ExprNode.Call(c0, _t0, _a0) => c0, _ => -1 };
    let targs: Vec[ast.TypeName] = match n { ast.ExprNode.Call(_c0, t0, _a0) => t0, _ => Vec() };
    let args: Vec[i32] = match n { ast.ExprNode.Call(_c0, _t0, a0) => a0, _ => Vec() };
    let rc: LiftExprResult = lift_expr(ctx, w, mod_path, imps, l0, exprs0, callee, tmp0, sp, false);
    if !rc.ok { return rc; }
    let mut p: ast.ExprPool = rc.exprs;
    let mut pre: Vec[ast.Stmt] = Vec();
    let mut i: i32 = 0;
    while i < rc.prefix.len() { pre.push(rc.prefix.get(i)); i = i + 1; }
    let mut changed: bool = rc.id != callee;
    let mut out_args: Vec[i32] = Vec();
    let mut tmp: i32 = rc.next_tmp;
    i = 0;
    while i < args.len() {
      let ra: LiftExprResult = lift_expr(ctx, w, mod_path, imps, l0, p, args.get(i), tmp, sp, false);
      if !ra.ok { return ra; }
      p = ra.exprs;
      tmp = ra.next_tmp;
      let mut j: i32 = 0;
      while j < ra.prefix.len() { pre.push(ra.prefix.get(j)); j = j + 1; }
      if ra.id != args.get(i) { changed = true; }
      out_args.push(ra.id);
      i = i + 1;
    }
    if !changed && pre.len() == 0 { return lift_ok(p, Vec(), eid, tmp); }
    let r1: ast.AddExprResult = add_expr(p, ast.ExprNode.Call(rc.id, targs, out_args), sp);
    return lift_ok(r1.pool, pre, r1.id, tmp);
  }

  if match n { ast.ExprNode.StructLit(_tyex, _fields) => true, _ => false } {
    let tyex: i32 = match n { ast.ExprNode.StructLit(t0, _fs0) => t0, _ => -1 };
    let fields: Vec[ast.StructLitField] = match n { ast.ExprNode.StructLit(_t0, fs0) => fs0, _ => Vec() };
    // Struct literals evaluate all field expressions eagerly; lift is safe.
    let mut p: ast.ExprPool = exprs0;
    let mut pre: Vec[ast.Stmt] = Vec();
    let mut tmp: i32 = tmp0;
    let mut changed: bool = false;
    let mut out_fields: Vec[ast.StructLitField] = Vec();
    let mut i: i32 = 0;
    while i < fields.len() {
      let f0: ast.StructLitField = fields.get(i);
      let rf: LiftExprResult = lift_expr(ctx, w, mod_path, imps, l0, p, f0.expr, tmp, sp, false);
      if !rf.ok { return rf; }
      p = rf.exprs;
      tmp = rf.next_tmp;
      let mut j: i32 = 0;
      while j < rf.prefix.len() { pre.push(rf.prefix.get(j)); j = j + 1; }
      if rf.id != f0.expr { changed = true; }
      out_fields.push(ast.StructLitField { name: f0.name, expr: rf.id });
      i = i + 1;
    }
    if !changed && pre.len() == 0 { return lift_ok(p, Vec(), eid, tmp); }
    let r1: ast.AddExprResult = add_expr(p, ast.ExprNode.StructLit(tyex, out_fields), sp);
    return lift_ok(r1.pool, pre, r1.id, tmp);
  }

  // Block expression: `{ stmt*; tail }`
  // - if it contains await, inline the statement list into prefix and return the tail value
  // - otherwise keep it as-is
  if match n { ast.ExprNode.Block(_b) => true, _ => false } {
    if !expr_has_await(exprs0, eid) { return lift_ok(exprs0, Vec(), eid, tmp0); }
    let b: ast.ExprBlock = match n { ast.ExprNode.Block(b0) => b0, _ => ast.ExprBlock { stmts: Vec(), has_tail: false, tail: -1 } };
    let rn: NormalizeInnerResult = normalize_async_stmt_list(ctx, w, mod_path, imps, exprs0, b.stmts, tmp0, l0);
    if !rn.ok { return lift_err(rn.exprs, rn.err, rn.next_tmp); }
    let mut p: ast.ExprPool = rn.exprs;
    let mut tmp: i32 = rn.next_tmp;
    let mut pre: Vec[ast.Stmt] = rn.stmts;
    if b.has_tail {
      let rt: LiftExprResult = lift_expr(ctx, w, mod_path, imps, l0, p, b.tail, tmp, sp, allow_root_await);
      if !rt.ok { return rt; }
      p = rt.exprs;
      tmp = rt.next_tmp;
      let mut j: i32 = 0;
      while j < rt.prefix.len() { pre.push(rt.prefix.get(j)); j = j + 1; }
      return lift_ok(p, pre, rt.id, tmp);
    }
    // unit value: keep an empty block expression as the value.
    let r_unit: ast.AddExprResult = add_expr(p, ast.ExprNode.Block(ast.ExprBlock { stmts: Vec(), has_tail: false, tail: -1 }), sp);
    return lift_ok(r_unit.pool, pre, r_unit.id, tmp);
  }

  if match n { ast.ExprNode.If(_c, _t, _e) => true, _ => false } {
    if !expr_has_await(exprs0, eid) { return lift_ok(exprs0, Vec(), eid, tmp0); }
    let c0: i32 = match n { ast.ExprNode.If(c, _t, _e) => c, _ => -1 };
    let t0: i32 = match n { ast.ExprNode.If(_c, t, _e) => t, _ => -1 };
    let e0: i32 = match n { ast.ExprNode.If(_c, _t, e) => e, _ => -1 };
    let ir: InferExprTyResult = infer_expr_ty(ctx, w, mod_path, imps, exprs0, l0, eid, sp);
    if !ir.ok { return lift_err(exprs0, "await lowering: cannot infer if-expression type: ".concat(ir.err), tmp0); }

    let rc: LiftExprResult = lift_expr(ctx, w, mod_path, imps, l0, exprs0, c0, tmp0, sp, false);
    if !rc.ok { return rc; }
    let mut p: ast.ExprPool = rc.exprs;
    let mut pre: Vec[ast.Stmt] = Vec();
    let mut i: i32 = 0;
    while i < rc.prefix.len() { pre.push(rc.prefix.get(i)); i = i + 1; }
    let mut tmp: i32 = rc.next_tmp;

    let outnm: String = match_out_name(tmp);
    tmp = tmp + 1;
    let runinit: ast.AddExprResult = add_uninit_id(p, sp, ir.ty);
    p = runinit.pool;
    pre.push(ast.Stmt.Let(sp, true, outnm, false, ast.TypeName { parts: Vec(), args: Vec() }, runinit.id));

    let mut l_out: Locals = locals_decl(l0, outnm, ir.ty, true);

    let mut then_ss: Vec[ast.Stmt] = Vec();
    then_ss.push(ast.Stmt.Assign(sp, outnm, t0));
    let rt: NormalizeInnerResult = normalize_async_stmt_list(ctx, w, mod_path, imps, p, then_ss, tmp, l_out);
    if !rt.ok { return lift_err(rt.exprs, rt.err, rt.next_tmp); }
    p = rt.exprs;
    tmp = rt.next_tmp;

    let mut else_ss: Vec[ast.Stmt] = Vec();
    else_ss.push(ast.Stmt.Assign(sp, outnm, e0));
    let re: NormalizeInnerResult = normalize_async_stmt_list(ctx, w, mod_path, imps, p, else_ss, tmp, l_out);
    if !re.ok { return lift_err(re.exprs, re.err, re.next_tmp); }
    p = re.exprs;
    tmp = re.next_tmp;

    pre.push(ast.Stmt.IfStmt(sp, rc.id, ast.Block { stmts: rt.stmts }, true, ast.Block { stmts: re.stmts }));
    let rid: ast.AddExprResult = add_ident(p, sp, outnm);
    return lift_ok(rid.pool, pre, rid.id, tmp);
  }

  if match n { ast.ExprNode.Match(_s, _arms) => true, _ => false } {
    if !expr_has_await(exprs0, eid) { return lift_ok(exprs0, Vec(), eid, tmp0); }
    let scrut0: i32 = match n { ast.ExprNode.Match(s0, _a0) => s0, _ => -1 };
    let arms0: Vec[ast.MatchArm] = match n { ast.ExprNode.Match(_s0, a0) => a0, _ => Vec() };
    let ir: InferExprTyResult = infer_expr_ty(ctx, w, mod_path, imps, exprs0, l0, eid, sp);
    if !ir.ok { return lift_err(exprs0, "await lowering: cannot infer match-expression type: ".concat(ir.err), tmp0); }

    let outnm: String = match_out_name(tmp0);
    let mut tmp: i32 = tmp0 + 1;
    let rs: LiftExprResult = lift_expr(ctx, w, mod_path, imps, l0, exprs0, scrut0, tmp, sp, false);
    if !rs.ok { return rs; }
    let mut p: ast.ExprPool = rs.exprs;
    tmp = rs.next_tmp;
    let mut pre: Vec[ast.Stmt] = Vec();
    let mut i: i32 = 0;
    while i < rs.prefix.len() { pre.push(rs.prefix.get(i)); i = i + 1; }

    let runinit: ast.AddExprResult = add_uninit_id(p, sp, ir.ty);
    p = runinit.pool;
    pre.push(ast.Stmt.Let(sp, true, outnm, false, ast.TypeName { parts: Vec(), args: Vec() }, runinit.id));

    let mut l_out: Locals = locals_decl(l0, outnm, ir.ty, true);
    let rr: NormalizeInnerResult = normalize_match_with_await_in_stmt_ctx(ctx, w, mod_path, imps, p, sp, rs.id, arms0, tmp, 0, outnm, l_out);
    if !rr.ok { return lift_err(rr.exprs, rr.err, rr.next_tmp); }
    p = rr.exprs;
    tmp = rr.next_tmp;
    i = 0;
    while i < rr.stmts.len() { pre.push(rr.stmts.get(i)); i = i + 1; }
    let rid: ast.AddExprResult = add_ident(p, sp, outnm);
    return lift_ok(rid.pool, pre, rid.id, tmp);
  }

  if match n { ast.ExprNode.TryBlock(_bid) => true, _ => false } {
    if !expr_has_await(exprs0, eid) { return lift_ok(exprs0, Vec(), eid, tmp0); }
    let bid0: i32 = match n { ast.ExprNode.TryBlock(x0) => x0, _ => -1 };
    let bn: ast.ExprNode = ast.expr_pool_get(exprs0, bid0);
    if !match bn { ast.ExprNode.Block(_b0) => true, _ => false } {
      return lift_err(exprs0, "internal: try block operand is not block expression", tmp0);
    }
    let b0: ast.ExprBlock = match bn { ast.ExprNode.Block(eb0) => eb0, _ => ast.ExprBlock { stmts: Vec(), has_tail: false, tail: -1 } };
    let ir: InferExprTyResult = infer_expr_ty(ctx, w, mod_path, imps, exprs0, l0, eid, sp);
    if !ir.ok { return lift_err(exprs0, "await lowering: cannot infer try-block type: ".concat(ir.err), tmp0); }

    let outnm: String = try_out_name(tmp0);
    let mut tmp: i32 = tmp0 + 1;
    let runinit: ast.AddExprResult = add_uninit_id(exprs0, sp, ir.ty);
    let mut p: ast.ExprPool = runinit.pool;
    let mut pre: Vec[ast.Stmt] = Vec();
    pre.push(ast.Stmt.Let(sp, true, outnm, false, ast.TypeName { parts: Vec(), args: Vec() }, runinit.id));
    pre.push(ast.Stmt.TryBegin(sp, outnm));

    let l_try: Locals = locals_try_push(l0, ir.ty);
    let rn: NormalizeInnerResult = normalize_async_stmt_list(ctx, w, mod_path, imps, p, b0.stmts, tmp, l_try);
    if !rn.ok { return lift_err(rn.exprs, rn.err, rn.next_tmp); }
    p = rn.exprs;
    tmp = rn.next_tmp;

    let mut i: i32 = 0;
    while i < rn.stmts.len() {
      pre.push(rn.stmts.get(i));
      i = i + 1;
    }

    let mut tail_id: i32 = -1;
    if b0.has_tail {
      let rt: LiftExprResult = lift_expr(ctx, w, mod_path, imps, l_try, p, b0.tail, tmp, sp, true);
      if !rt.ok { return rt; }
      p = rt.exprs;
      tmp = rt.next_tmp;
      i = 0;
      while i < rt.prefix.len() {
        pre.push(rt.prefix.get(i));
        i = i + 1;
      }
      tail_id = rt.id;
    }

    let r_blk: ast.AddExprResult = add_expr(p, ast.ExprNode.Block(ast.ExprBlock { stmts: Vec(), has_tail: b0.has_tail, tail: tail_id }), sp);
    let r_tr: ast.AddExprResult = add_expr(r_blk.pool, ast.ExprNode.TryBlock(r_blk.id), sp);
    pre.push(ast.Stmt.Assign(sp, outnm, r_tr.id));
    pre.push(ast.Stmt.TryEnd(sp));

    let rid: ast.AddExprResult = add_ident(r_tr.pool, sp, outnm);
    return lift_ok(rid.pool, pre, rid.id, tmp);
  }

  if match n { ast.ExprNode.MacroCall(_c, _t, _args) => true, _ => false } {
    let callee: i32 = match n { ast.ExprNode.MacroCall(c0, _t0, _a0) => c0, _ => -1 };
    let targs: Vec[ast.TypeName] = match n { ast.ExprNode.MacroCall(_c0, t0, _a0) => t0, _ => Vec() };
    let args: Vec[i32] = match n { ast.ExprNode.MacroCall(_c0, _t0, a0) => a0, _ => Vec() };
    let rc: LiftExprResult = lift_expr(ctx, w, mod_path, imps, l0, exprs0, callee, tmp0, sp, false);
    if !rc.ok { return rc; }
    let mut p: ast.ExprPool = rc.exprs;
    let mut pre: Vec[ast.Stmt] = Vec();
    let mut i: i32 = 0;
    while i < rc.prefix.len() { pre.push(rc.prefix.get(i)); i = i + 1; }
    let mut changed: bool = rc.id != callee;
    let mut out_args: Vec[i32] = Vec();
    let mut tmp: i32 = rc.next_tmp;
    i = 0;
    while i < args.len() {
      let ra: LiftExprResult = lift_expr(ctx, w, mod_path, imps, l0, p, args.get(i), tmp, sp, false);
      if !ra.ok { return ra; }
      p = ra.exprs;
      tmp = ra.next_tmp;
      let mut j: i32 = 0;
      while j < ra.prefix.len() { pre.push(ra.prefix.get(j)); j = j + 1; }
      if ra.id != args.get(i) { changed = true; }
      out_args.push(ra.id);
      i = i + 1;
    }
    if !changed && pre.len() == 0 { return lift_ok(p, Vec(), eid, tmp); }
    let r1: ast.AddExprResult = add_expr(p, ast.ExprNode.MacroCall(rc.id, targs, out_args), sp);
    return lift_ok(r1.pool, pre, r1.id, tmp);
  }

  if match n { ast.ExprNode.Try(_a) => true, _ => false } {
    let a: i32 = match n { ast.ExprNode.Try(x) => x, _ => -1 };
    let ra: LiftExprResult = lift_expr(ctx, w, mod_path, imps, l0, exprs0, a, tmp0, sp, false);
    if !ra.ok { return ra; }
    if ra.prefix.len() == 0 && ra.id == a { return lift_ok(ra.exprs, Vec(), eid, ra.next_tmp); }
    let r1: ast.AddExprResult = add_expr(ra.exprs, ast.ExprNode.Try(ra.id), sp);
    return lift_ok(r1.pool, ra.prefix, r1.id, ra.next_tmp);
  }
  if match n { ast.ExprNode.Await(_a) => true, _ => false } {
    let a: i32 = match n { ast.ExprNode.Await(x) => x, _ => -1 };
    let ra: LiftExprResult = lift_expr(ctx, w, mod_path, imps, l0, exprs0, a, tmp0, sp, false);
    if !ra.ok { return ra; }
    if ra.prefix.len() == 0 && ra.id == a { return lift_ok(ra.exprs, Vec(), eid, ra.next_tmp); }
    let r1: ast.AddExprResult = add_expr(ra.exprs, ast.ExprNode.Await(ra.id), sp);
    return lift_ok(r1.pool, ra.prefix, r1.id, ra.next_tmp);
  }

  // Defensive fallback (should be unreachable if ExprNode is exhaustive).
  return lift_ok(exprs0, Vec(), eid, tmp0);
}

pub struct NormalizeResult { pub ok: bool, pub err: String, pub exprs: ast.ExprPool, pub body: ast.Block }

struct NormalizeInnerResult { ok: bool, err: String, exprs: ast.ExprPool, stmts: Vec[ast.Stmt], next_tmp: i32 }

fn normalize_match_with_await_in_stmt_ctx(
  ctx: Ctx,
  w: World,
  mod_path: String,
  imps: Imports,
  exprs0: ast.ExprPool,
  sp: ast.Span,
  scrut0: i32,
  arms0: Vec[ast.MatchArm],
  tmp0: i32,
  kind: i32,
  outnm: String,
  l0: Locals,
) -> NormalizeInnerResult {
  // kind: 0 = assign to outnm, 1 = exprstmt, 2 = return
  if arms0.len() == 0 {
    return NormalizeInnerResult { ok: false, err: "match must have at least one arm", exprs: exprs0, stmts: Vec(), next_tmp: tmp0 };
  }
  if kind == 0 && outnm == "" {
    return NormalizeInnerResult { ok: false, err: "internal: match lowering missing out name", exprs: exprs0, stmts: Vec(), next_tmp: tmp0 };
  }

  let mut p: ast.ExprPool = exprs0;
  let mut tmp: i32 = tmp0;
  let mut out: Vec[ast.Stmt] = Vec();

  // Evaluate scrutinee once (lift awaits out first).
  let rs: LiftExprResult = lift_expr(ctx, w, mod_path, imps, l0, p, scrut0, tmp, sp, false);
  if !rs.ok { return NormalizeInnerResult { ok: false, err: rs.err, exprs: rs.exprs, stmts: Vec(), next_tmp: rs.next_tmp }; }
  p = rs.exprs;
  tmp = rs.next_tmp;
  let mut j0: i32 = 0;
  while j0 < rs.prefix.len() { out.push(rs.prefix.get(j0)); j0 = j0 + 1; }

  let scrutnm: String = match_scrut_name(tmp);
  tmp = tmp + 1;
  out.push(ast.Stmt.Let(sp, false, scrutnm, false, ast.TypeName { parts: Vec(), args: Vec() }, rs.id));
  let r_scrut: ast.AddExprResult = add_ident(p, sp, scrutnm);
  p = r_scrut.pool;
  let scrut_id: i32 = r_scrut.id;
  let mut l_match: Locals = l0;
  let sr_ty: InferExprTyResult = infer_expr_ty(ctx, w, mod_path, imps, p, l0, rs.id, sp);
  if sr_ty.ok {
    l_match = locals_decl(l_match, scrutnm, sr_ty.ty, false);
  }

  // Else branch: panic for non-exhaustive match.
  let r_panic: ast.AddExprResult = mk_panic_call(p, sp, "non-exhaustive match in async await lowering");
  p = r_panic.pool;
  let mut else_blk: ast.Block = ast.Block { stmts: Vec() };
  else_blk.stmts.push(ast.Stmt.ExprStmt(sp, r_panic.id));

  let mut ai: i32 = arms0.len() - 1;
  while ai >= 0 {
    let arm: ast.MatchArm = arms0.get(ai);
    let cr: LiftExprResult = mk_match_cond(p, sp, scrut_id, arm.pat);
    if !cr.ok { return NormalizeInnerResult { ok: false, err: cr.err, exprs: cr.exprs, stmts: Vec(), next_tmp: tmp }; }
    p = cr.exprs;
    let br: LiftExprResult = mk_match_binds(p, sp, scrut_id, arm.pat);
    if !br.ok { return NormalizeInnerResult { ok: false, err: br.err, exprs: br.exprs, stmts: Vec(), next_tmp: tmp }; }
    p = br.exprs;

    let mut then_ss0: Vec[ast.Stmt] = Vec();
    let mut bi0: i32 = 0;
    while bi0 < br.prefix.len() { then_ss0.push(br.prefix.get(bi0)); bi0 = bi0 + 1; }

    let mut arm_stmt: ast.Stmt = ast.Stmt.ExprStmt(sp, arm.expr);
    if kind == 0 { arm_stmt = ast.Stmt.Assign(sp, outnm, arm.expr); }
    else if kind == 2 { arm_stmt = ast.Stmt.ReturnStmt(sp, true, arm.expr); }
    then_ss0.push(arm_stmt);

    let rt: NormalizeInnerResult = normalize_async_stmt_list(ctx, w, mod_path, imps, p, then_ss0, tmp, l_match);
    if !rt.ok { return rt; }
    p = rt.exprs;
    tmp = rt.next_tmp;

    let ifst: ast.Stmt = ast.Stmt.IfStmt(sp, cr.id, ast.Block { stmts: rt.stmts }, true, else_blk);
    else_blk = ast.Block { stmts: Vec() };
    else_blk.stmts.push(ifst);
    ai = ai - 1;
  }

  out.push(else_blk.stmts.get(0));
  return NormalizeInnerResult { ok: true, err: "", exprs: p, stmts: out, next_tmp: tmp };
}

fn normalize_async_stmt_list(
  ctx: Ctx,
  w: World,
  mod_path: String,
  imps: Imports,
  exprs0: ast.ExprPool,
  ss0: Vec[ast.Stmt],
  tmp0: i32,
  l0: Locals,
) -> NormalizeInnerResult {
  let mut p: ast.ExprPool = exprs0;
  let mut out: Vec[ast.Stmt] = Vec();
  let mut tmp: i32 = tmp0;
  let mut l_cur: Locals = l0;

  let mut i: i32 = 0;
  while i < ss0.len() {
    let st: ast.Stmt = ss0.get(i);
    let sp: ast.Span = stmt_span(st);

    if match st { ast.Stmt.Let(_sp, _m, _n, _ha, _ann, _e) => true, _ => false } {
      let is_mut: bool = match st { ast.Stmt.Let(_sp, m0, _n, _ha, _ann, _e) => m0, _ => false };
      let name0: String = match st { ast.Stmt.Let(_sp, _m, n0, _ha, _ann, _e) => n0, _ => "" };
      let has_ann: bool = match st { ast.Stmt.Let(_sp, _m, _n, ha0, _ann, _e) => ha0, _ => false };
      let ann0: ast.TypeName = match st { ast.Stmt.Let(_sp, _m, _n, _ha, an0, _e) => an0, _ => ast.TypeName { parts: Vec(), args: Vec() } };
      let init0: i32 = match st { ast.Stmt.Let(_sp, _m, _n, _ha, _ann, e0) => e0, _ => -1 };

      // Desugar `let x: T = if cond { ...await... } else { ... }` into a statement-level if.
      // This avoids placing await across expression-level control-flow joins.
      let in0: ast.ExprNode = ast.expr_pool_get(p, init0);
      if false && match in0 { ast.ExprNode.If(_c, _t, _e) => true, _ => false } && expr_has_await(p, init0) {
        if !has_ann {
          return NormalizeInnerResult { ok: false, err: "await inside if-expression requires explicit let annotation type", exprs: p, stmts: Vec(), next_tmp: tmp };
        }
        let cond0: i32 = match in0 { ast.ExprNode.If(c0, _t0, _e0) => c0, _ => -1 };
        let then0: i32 = match in0 { ast.ExprNode.If(_c0, t0, _e0) => t0, _ => -1 };
        let else0: i32 = match in0 { ast.ExprNode.If(_c0, _t0, e0) => e0, _ => -1 };

        let tmpnm: String = tmp_name(tmp);
        tmp = tmp + 1;
        let r_cal: ast.AddExprResult = add_ident(p, sp, "@uninit");
        p = r_cal.pool;
        let r_call: ast.AddExprResult = add_expr(p, ast.ExprNode.Call(r_cal.id, Vec(), Vec()), sp);
        p = r_call.pool;
        out.push(ast.Stmt.Let(sp, true, tmpnm, true, ann0, r_call.id));

        let rc: LiftExprResult = lift_expr(ctx, w, mod_path, imps, l0, p, cond0, tmp, sp, false);
        if !rc.ok { return NormalizeInnerResult { ok: false, err: rc.err, exprs: rc.exprs, stmts: Vec(), next_tmp: rc.next_tmp }; }
        p = rc.exprs;
        tmp = rc.next_tmp;
        let mut j0: i32 = 0;
        while j0 < rc.prefix.len() { out.push(rc.prefix.get(j0)); j0 = j0 + 1; }

        let mut then_ss0: Vec[ast.Stmt] = Vec();
        then_ss0.push(ast.Stmt.Assign(sp, tmpnm, then0));
        let rt: NormalizeInnerResult = normalize_async_stmt_list(ctx, w, mod_path, imps, p, then_ss0, tmp, l0);
        if !rt.ok { return rt; }
        p = rt.exprs;
        tmp = rt.next_tmp;

        let mut else_ss0: Vec[ast.Stmt] = Vec();
        else_ss0.push(ast.Stmt.Assign(sp, tmpnm, else0));
        let re: NormalizeInnerResult = normalize_async_stmt_list(ctx, w, mod_path, imps, p, else_ss0, tmp, l0);
        if !re.ok { return re; }
        p = re.exprs;
        tmp = re.next_tmp;

        out.push(ast.Stmt.IfStmt(sp, rc.id, ast.Block { stmts: rt.stmts }, true, ast.Block { stmts: re.stmts }));

        let r_tmp: ast.AddExprResult = add_ident(p, sp, tmpnm);
        p = r_tmp.pool;
        out.push(ast.Stmt.Let(sp, is_mut, name0, true, ann0, r_tmp.id));
        i = i + 1;
        continue;
      }

      // Desugar `let x: T = match scrut { ...await... }` into a statement-level if chain.
      // This keeps await points discoverable by the statement-level async CFG builder.
      if false && match in0 { ast.ExprNode.Match(_s, _arms) => true, _ => false } && expr_has_await(p, init0) {
        if !has_ann {
          return NormalizeInnerResult { ok: false, err: "await inside match-expression requires explicit let annotation type", exprs: p, stmts: Vec(), next_tmp: tmp };
        }
        let scrut0: i32 = match in0 { ast.ExprNode.Match(s0, _a0) => s0, _ => -1 };
        let arms0: Vec[ast.MatchArm] = match in0 { ast.ExprNode.Match(_s0, a0) => a0, _ => Vec() };

        let outnm: String = match_out_name(tmp);
        tmp = tmp + 1;
        let r_cal: ast.AddExprResult = add_ident(p, sp, "@uninit");
        p = r_cal.pool;
        let r_call: ast.AddExprResult = add_expr(p, ast.ExprNode.Call(r_cal.id, Vec(), Vec()), sp);
        p = r_call.pool;
        out.push(ast.Stmt.Let(sp, true, outnm, true, ann0, r_call.id));

        let rr: NormalizeInnerResult = normalize_match_with_await_in_stmt_ctx(ctx, w, mod_path, imps, p, sp, scrut0, arms0, tmp, 0, outnm, l0);
        if !rr.ok { return rr; }
        p = rr.exprs;
        tmp = rr.next_tmp;
        let mut k0: i32 = 0;
        while k0 < rr.stmts.len() { out.push(rr.stmts.get(k0)); k0 = k0 + 1; }

        let r_out: ast.AddExprResult = add_ident(p, sp, outnm);
        p = r_out.pool;
        out.push(ast.Stmt.Let(sp, is_mut, name0, true, ann0, r_out.id));
        i = i + 1;
        continue;
      }

      let rn: LiftExprResult = lift_expr(ctx, w, mod_path, imps, l_cur, p, init0, tmp, sp, true);
      if !rn.ok { return NormalizeInnerResult { ok: false, err: rn.err, exprs: rn.exprs, stmts: Vec(), next_tmp: rn.next_tmp }; }
      p = rn.exprs;
      tmp = rn.next_tmp;
      let mut j: i32 = 0;
      while j < rn.prefix.len() { out.push(rn.prefix.get(j)); j = j + 1; }
      out.push(ast.Stmt.Let(sp, is_mut, name0, has_ann, ann0, rn.id));
      let mut vty: i32 = -1;
      if has_ann {
        let tr0: ResolveTyResult = resolve_type(ctx, w, mod_path, imps, l_cur.type_params, ann0);
        if !tr0.ok {
          return NormalizeInnerResult { ok: false, err: "await lowering: failed to resolve let annotation", exprs: p, stmts: Vec(), next_tmp: tmp };
        }
        vty = strip_range_only(tr0.ctx, tr0.idx);
      } else {
        let ir0: InferExprTyResult = infer_expr_ty(ctx, w, mod_path, imps, p, l_cur, rn.id, sp);
        if !ir0.ok {
          return NormalizeInnerResult { ok: false, err: "await lowering: failed to infer let type: ".concat(ir0.err), exprs: p, stmts: Vec(), next_tmp: tmp };
        }
        vty = ir0.ty;
      }
      l_cur = locals_decl(l_cur, name0, vty, is_mut);
      i = i + 1;
      continue;
    }

    if match st { ast.Stmt.ExprStmt(_sp, _e) => true, _ => false } {
      let e0: i32 = match st { ast.Stmt.ExprStmt(_sp, e0) => e0, _ => -1 };
      // Desugar `if cond { ...await... } else { ... }` expression used as a statement.
      let en0: ast.ExprNode = ast.expr_pool_get(p, e0);
      if false && match en0 { ast.ExprNode.If(_c, _t, _e) => true, _ => false } && expr_has_await(p, e0) {
        let cond0: i32 = match en0 { ast.ExprNode.If(c0, _t0, _e0) => c0, _ => -1 };
        let then0: i32 = match en0 { ast.ExprNode.If(_c0, t0, _e0) => t0, _ => -1 };
        let else0: i32 = match en0 { ast.ExprNode.If(_c0, _t0, e0) => e0, _ => -1 };
        let rc: LiftExprResult = lift_expr(ctx, w, mod_path, imps, l0, p, cond0, tmp, sp, false);
        if !rc.ok { return NormalizeInnerResult { ok: false, err: rc.err, exprs: rc.exprs, stmts: Vec(), next_tmp: rc.next_tmp }; }
        p = rc.exprs;
        tmp = rc.next_tmp;
        let mut j0: i32 = 0;
        while j0 < rc.prefix.len() { out.push(rc.prefix.get(j0)); j0 = j0 + 1; }

        let mut then_ss0: Vec[ast.Stmt] = Vec();
        then_ss0.push(ast.Stmt.ExprStmt(sp, then0));
        let rt: NormalizeInnerResult = normalize_async_stmt_list(ctx, w, mod_path, imps, p, then_ss0, tmp, l0);
        if !rt.ok { return rt; }
        p = rt.exprs;
        tmp = rt.next_tmp;

        let mut else_ss0: Vec[ast.Stmt] = Vec();
        else_ss0.push(ast.Stmt.ExprStmt(sp, else0));
        let re: NormalizeInnerResult = normalize_async_stmt_list(ctx, w, mod_path, imps, p, else_ss0, tmp, l0);
        if !re.ok { return re; }
        p = re.exprs;
        tmp = re.next_tmp;

        out.push(ast.Stmt.IfStmt(sp, rc.id, ast.Block { stmts: rt.stmts }, true, ast.Block { stmts: re.stmts }));
        i = i + 1;
        continue;
      }

      // Desugar `match scrut { ...await... }` expression used as a statement.
      if false && match en0 { ast.ExprNode.Match(_s, _arms) => true, _ => false } && expr_has_await(p, e0) {
        let scrut0: i32 = match en0 { ast.ExprNode.Match(s0, _a0) => s0, _ => -1 };
        let arms0: Vec[ast.MatchArm] = match en0 { ast.ExprNode.Match(_s0, a0) => a0, _ => Vec() };
        let rr: NormalizeInnerResult = normalize_match_with_await_in_stmt_ctx(ctx, w, mod_path, imps, p, sp, scrut0, arms0, tmp, 1, "", l0);
        if !rr.ok { return rr; }
        p = rr.exprs;
        tmp = rr.next_tmp;
        let mut k0: i32 = 0;
        while k0 < rr.stmts.len() { out.push(rr.stmts.get(k0)); k0 = k0 + 1; }
        i = i + 1;
        continue;
      }
      let rn: LiftExprResult = lift_expr(ctx, w, mod_path, imps, l_cur, p, e0, tmp, sp, true);
      if !rn.ok { return NormalizeInnerResult { ok: false, err: rn.err, exprs: rn.exprs, stmts: Vec(), next_tmp: rn.next_tmp }; }
      p = rn.exprs;
      tmp = rn.next_tmp;
      let mut j: i32 = 0;
      while j < rn.prefix.len() { out.push(rn.prefix.get(j)); j = j + 1; }
      out.push(ast.Stmt.ExprStmt(sp, rn.id));
      i = i + 1;
      continue;
    }

    if match st { ast.Stmt.ReturnStmt(_sp, _h, _e) => true, _ => false } {
      let has0: bool = match st { ast.Stmt.ReturnStmt(_sp, h0, _e) => h0, _ => false };
      let e0: i32 = match st { ast.Stmt.ReturnStmt(_sp, _h, e0) => e0, _ => -1 };
      if !has0 {
        out.push(st);
        i = i + 1;
        continue;
      }
      // Desugar `return if cond { ...await... } else { ... }`.
      let en0: ast.ExprNode = ast.expr_pool_get(p, e0);
      if false && match en0 { ast.ExprNode.If(_c, _t, _e) => true, _ => false } && expr_has_await(p, e0) {
        let cond0: i32 = match en0 { ast.ExprNode.If(c0, _t0, _e0) => c0, _ => -1 };
        let then0: i32 = match en0 { ast.ExprNode.If(_c0, t0, _e0) => t0, _ => -1 };
        let else0: i32 = match en0 { ast.ExprNode.If(_c0, _t0, e0) => e0, _ => -1 };
        let rc: LiftExprResult = lift_expr(ctx, w, mod_path, imps, l0, p, cond0, tmp, sp, false);
        if !rc.ok { return NormalizeInnerResult { ok: false, err: rc.err, exprs: rc.exprs, stmts: Vec(), next_tmp: rc.next_tmp }; }
        p = rc.exprs;
        tmp = rc.next_tmp;
        let mut j0: i32 = 0;
        while j0 < rc.prefix.len() { out.push(rc.prefix.get(j0)); j0 = j0 + 1; }

        let mut then_ss0: Vec[ast.Stmt] = Vec();
        then_ss0.push(ast.Stmt.ReturnStmt(sp, true, then0));
        let rt: NormalizeInnerResult = normalize_async_stmt_list(ctx, w, mod_path, imps, p, then_ss0, tmp, l0);
        if !rt.ok { return rt; }
        p = rt.exprs;
        tmp = rt.next_tmp;

        let mut else_ss0: Vec[ast.Stmt] = Vec();
        else_ss0.push(ast.Stmt.ReturnStmt(sp, true, else0));
        let re: NormalizeInnerResult = normalize_async_stmt_list(ctx, w, mod_path, imps, p, else_ss0, tmp, l0);
        if !re.ok { return re; }
        p = re.exprs;
        tmp = re.next_tmp;

        out.push(ast.Stmt.IfStmt(sp, rc.id, ast.Block { stmts: rt.stmts }, true, ast.Block { stmts: re.stmts }));
        i = i + 1;
        continue;
      }

      // Desugar `return match scrut { ...await... }`.
      if false && match en0 { ast.ExprNode.Match(_s, _arms) => true, _ => false } && expr_has_await(p, e0) {
        let scrut0: i32 = match en0 { ast.ExprNode.Match(s0, _a0) => s0, _ => -1 };
        let arms0: Vec[ast.MatchArm] = match en0 { ast.ExprNode.Match(_s0, a0) => a0, _ => Vec() };
        let rr: NormalizeInnerResult = normalize_match_with_await_in_stmt_ctx(ctx, w, mod_path, imps, p, sp, scrut0, arms0, tmp, 2, "", l0);
        if !rr.ok { return rr; }
        p = rr.exprs;
        tmp = rr.next_tmp;
        let mut k0: i32 = 0;
        while k0 < rr.stmts.len() { out.push(rr.stmts.get(k0)); k0 = k0 + 1; }
        i = i + 1;
        continue;
      }
      let rn: LiftExprResult = lift_expr(ctx, w, mod_path, imps, l_cur, p, e0, tmp, sp, true);
      if !rn.ok { return NormalizeInnerResult { ok: false, err: rn.err, exprs: rn.exprs, stmts: Vec(), next_tmp: rn.next_tmp }; }
      p = rn.exprs;
      tmp = rn.next_tmp;
      let mut j: i32 = 0;
      while j < rn.prefix.len() { out.push(rn.prefix.get(j)); j = j + 1; }
      out.push(ast.Stmt.ReturnStmt(sp, true, rn.id));
      i = i + 1;
      continue;
    }

    if match st { ast.Stmt.Assign(_sp, _n, _e) => true, _ => false } {
      let name0: String = match st { ast.Stmt.Assign(_sp, n0, _e) => n0, _ => "" };
      let e0: i32 = match st { ast.Stmt.Assign(_sp, _n, e0) => e0, _ => -1 };
      // Desugar `x = if cond { ...await... } else { ... }`.
      let en0: ast.ExprNode = ast.expr_pool_get(p, e0);
      if false && match en0 { ast.ExprNode.If(_c, _t, _e) => true, _ => false } && expr_has_await(p, e0) {
        let cond0: i32 = match en0 { ast.ExprNode.If(c0, _t0, _e0) => c0, _ => -1 };
        let then0: i32 = match en0 { ast.ExprNode.If(_c0, t0, _e0) => t0, _ => -1 };
        let else0: i32 = match en0 { ast.ExprNode.If(_c0, _t0, e0) => e0, _ => -1 };
        let rc: LiftExprResult = lift_expr(ctx, w, mod_path, imps, l0, p, cond0, tmp, sp, false);
        if !rc.ok { return NormalizeInnerResult { ok: false, err: rc.err, exprs: rc.exprs, stmts: Vec(), next_tmp: rc.next_tmp }; }
        p = rc.exprs;
        tmp = rc.next_tmp;
        let mut j0: i32 = 0;
        while j0 < rc.prefix.len() { out.push(rc.prefix.get(j0)); j0 = j0 + 1; }

        let mut then_ss0: Vec[ast.Stmt] = Vec();
        then_ss0.push(ast.Stmt.Assign(sp, name0, then0));
        let rt: NormalizeInnerResult = normalize_async_stmt_list(ctx, w, mod_path, imps, p, then_ss0, tmp, l0);
        if !rt.ok { return rt; }
        p = rt.exprs;
        tmp = rt.next_tmp;

        let mut else_ss0: Vec[ast.Stmt] = Vec();
        else_ss0.push(ast.Stmt.Assign(sp, name0, else0));
        let re: NormalizeInnerResult = normalize_async_stmt_list(ctx, w, mod_path, imps, p, else_ss0, tmp, l0);
        if !re.ok { return re; }
        p = re.exprs;
        tmp = re.next_tmp;

        out.push(ast.Stmt.IfStmt(sp, rc.id, ast.Block { stmts: rt.stmts }, true, ast.Block { stmts: re.stmts }));
        i = i + 1;
        continue;
      }
      let rn: LiftExprResult = lift_expr(ctx, w, mod_path, imps, l_cur, p, e0, tmp, sp, false);
      if !rn.ok { return NormalizeInnerResult { ok: false, err: rn.err, exprs: rn.exprs, stmts: Vec(), next_tmp: rn.next_tmp }; }
      p = rn.exprs;
      tmp = rn.next_tmp;
      let mut j: i32 = 0;
      while j < rn.prefix.len() { out.push(rn.prefix.get(j)); j = j + 1; }
      out.push(ast.Stmt.Assign(sp, name0, rn.id));
      i = i + 1;
      continue;
    }

    if match st { ast.Stmt.AssignField(_sp, _r, _f, _e) => true, _ => false } {
      let recv: String = match st { ast.Stmt.AssignField(_sp, r0, _f, _e) => r0, _ => "" };
      let f: String = match st { ast.Stmt.AssignField(_sp, _r, f0, _e) => f0, _ => "" };
      let e0: i32 = match st { ast.Stmt.AssignField(_sp, _r, _f, e0) => e0, _ => -1 };
      // Desugar `x.f = if cond { ...await... } else { ... }`.
      let en0: ast.ExprNode = ast.expr_pool_get(p, e0);
      if false && match en0 { ast.ExprNode.If(_c, _t, _e) => true, _ => false } && expr_has_await(p, e0) {
        let cond0: i32 = match en0 { ast.ExprNode.If(c0, _t0, _e0) => c0, _ => -1 };
        let then0: i32 = match en0 { ast.ExprNode.If(_c0, t0, _e0) => t0, _ => -1 };
        let else0: i32 = match en0 { ast.ExprNode.If(_c0, _t0, e0) => e0, _ => -1 };
        let rc: LiftExprResult = lift_expr(ctx, w, mod_path, imps, l0, p, cond0, tmp, sp, false);
        if !rc.ok { return NormalizeInnerResult { ok: false, err: rc.err, exprs: rc.exprs, stmts: Vec(), next_tmp: rc.next_tmp }; }
        p = rc.exprs;
        tmp = rc.next_tmp;
        let mut j0: i32 = 0;
        while j0 < rc.prefix.len() { out.push(rc.prefix.get(j0)); j0 = j0 + 1; }

        let mut then_ss0: Vec[ast.Stmt] = Vec();
        then_ss0.push(ast.Stmt.AssignField(sp, recv, f, then0));
        let rt: NormalizeInnerResult = normalize_async_stmt_list(ctx, w, mod_path, imps, p, then_ss0, tmp, l0);
        if !rt.ok { return rt; }
        p = rt.exprs;
        tmp = rt.next_tmp;

        let mut else_ss0: Vec[ast.Stmt] = Vec();
        else_ss0.push(ast.Stmt.AssignField(sp, recv, f, else0));
        let re: NormalizeInnerResult = normalize_async_stmt_list(ctx, w, mod_path, imps, p, else_ss0, tmp, l0);
        if !re.ok { return re; }
        p = re.exprs;
        tmp = re.next_tmp;

        out.push(ast.Stmt.IfStmt(sp, rc.id, ast.Block { stmts: rt.stmts }, true, ast.Block { stmts: re.stmts }));
        i = i + 1;
        continue;
      }
      let rn: LiftExprResult = lift_expr(ctx, w, mod_path, imps, l_cur, p, e0, tmp, sp, false);
      if !rn.ok { return NormalizeInnerResult { ok: false, err: rn.err, exprs: rn.exprs, stmts: Vec(), next_tmp: rn.next_tmp }; }
      p = rn.exprs;
      tmp = rn.next_tmp;
      let mut j: i32 = 0;
      while j < rn.prefix.len() { out.push(rn.prefix.get(j)); j = j + 1; }
      out.push(ast.Stmt.AssignField(sp, recv, f, rn.id));
      i = i + 1;
      continue;
    }

    if match st { ast.Stmt.IfStmt(_sp, _c, _t, _h, _e) => true, _ => false } {
      let cond: i32 = match st { ast.Stmt.IfStmt(_sp, c0, _t, _h, _e) => c0, _ => -1 };
      let then_b: ast.Block = match st { ast.Stmt.IfStmt(_sp, _c, t0, _h, _e) => t0, _ => ast.Block { stmts: Vec() } };
      let has_else: bool = match st { ast.Stmt.IfStmt(_sp, _c, _t, h0, _e) => h0, _ => false };
      let else_b: ast.Block = match st { ast.Stmt.IfStmt(_sp, _c, _t, _h, e0) => e0, _ => ast.Block { stmts: Vec() } };

      let rn: LiftExprResult = lift_expr(ctx, w, mod_path, imps, l_cur, p, cond, tmp, sp, false);
      if !rn.ok { return NormalizeInnerResult { ok: false, err: rn.err, exprs: rn.exprs, stmts: Vec(), next_tmp: rn.next_tmp }; }
      p = rn.exprs;
      tmp = rn.next_tmp;
      let mut j: i32 = 0;
      while j < rn.prefix.len() { out.push(rn.prefix.get(j)); j = j + 1; }

      let rt: NormalizeInnerResult = normalize_async_stmt_list(ctx, w, mod_path, imps, p, then_b.stmts, tmp, locals_enter(l_cur));
      if !rt.ok { return rt; }
      p = rt.exprs;
      tmp = rt.next_tmp;

      let mut else_stmts: Vec[ast.Stmt] = Vec();
      if has_else {
        let re: NormalizeInnerResult = normalize_async_stmt_list(ctx, w, mod_path, imps, p, else_b.stmts, tmp, locals_enter(l_cur));
        if !re.ok { return re; }
        p = re.exprs;
        tmp = re.next_tmp;
        else_stmts = re.stmts;
      }

      out.push(ast.Stmt.IfStmt(sp, rn.id, ast.Block { stmts: rt.stmts }, has_else, ast.Block { stmts: else_stmts }));
      i = i + 1;
      continue;
    }

    if match st { ast.Stmt.WhileStmt(_sp, _c, _b) => true, _ => false } {
      let cond: i32 = match st { ast.Stmt.WhileStmt(_sp, c0, _b) => c0, _ => -1 };
      let body0: ast.Block = match st { ast.Stmt.WhileStmt(_sp, _c0, b0) => b0, _ => ast.Block { stmts: Vec() } };

      let rn: LiftExprResult = lift_expr(ctx, w, mod_path, imps, l_cur, p, cond, tmp, sp, false);
      if !rn.ok { return NormalizeInnerResult { ok: false, err: rn.err, exprs: rn.exprs, stmts: Vec(), next_tmp: rn.next_tmp }; }
      p = rn.exprs;
      tmp = rn.next_tmp;
      let mut j: i32 = 0;
      while j < rn.prefix.len() { out.push(rn.prefix.get(j)); j = j + 1; }

      let rb: NormalizeInnerResult = normalize_async_stmt_list(ctx, w, mod_path, imps, p, body0.stmts, tmp, locals_enter(l_cur));
      if !rb.ok { return rb; }
      p = rb.exprs;
      tmp = rb.next_tmp;
      out.push(ast.Stmt.WhileStmt(sp, rn.id, ast.Block { stmts: rb.stmts }));
      i = i + 1;
      continue;
    }

    if match st { ast.Stmt.TryBegin(_sp, _out_name) => true, _ => false } {
      let out_name: String = match st { ast.Stmt.TryBegin(_sp, nm) => nm, _ => "" };
      let lk: LookupVarResult = locals_lookup(l_cur, out_name);
      if lk.found {
        l_cur = locals_try_push(l_cur, lk.ty);
      }
      out.push(st);
      i = i + 1;
      continue;
    }
    if match st { ast.Stmt.TryEnd(_sp) => true, _ => false } {
      l_cur = locals_try_pop(l_cur);
      out.push(st);
      i = i + 1;
      continue;
    }

    // break/continue passthrough
    if match st { ast.Stmt.Break(_sp) => true, ast.Stmt.Continue(_sp) => true, _ => false } {
      out.push(st);
      i = i + 1;
      continue;
    }

    // Unknown statement kind: preserve but reject if it contains await.
    if stmt_has_await_anywhere(p, st) {
      return NormalizeInnerResult { ok: false, err: "await in unsupported statement position".concat(""), exprs: p, stmts: Vec(), next_tmp: tmp };
    }
    out.push(st);
    i = i + 1;
  }

  return NormalizeInnerResult { ok: true, err: "", exprs: p, stmts: out, next_tmp: tmp };
}

pub fn normalize_async_block(
  ctx: Ctx,
  w: World,
  mod_path: String,
  imps: Imports,
  exprs0: ast.ExprPool,
  b0: ast.Block,
  l0: Locals,
) -> NormalizeResult {
  // Pre-pass: uniquify shadowed locals so async capture rewrite (`l_<name>`)
  // remains correct even when user code uses lexical shadowing.
  let rr: AsyncShadowRenameResult = async_shadow_rename_block(exprs0, b0, l0);
  let r: NormalizeInnerResult = normalize_async_stmt_list(ctx, w, mod_path, imps, rr.exprs, rr.body.stmts, 0, l0);
  if !r.ok { return NormalizeResult { ok: false, err: r.err, exprs: r.exprs, body: ast.Block { stmts: Vec() } }; }
  return NormalizeResult { ok: true, err: "", exprs: r.exprs, body: ast.Block { stmts: r.stmts } };
}
