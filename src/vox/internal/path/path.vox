// internal path helpers shared by compiler modules.

pub fn is_alpha_ascii(ch: i32) -> bool {
  if ch >= 65 && ch <= 90 { return true; }
  if ch >= 97 && ch <= 122 { return true; }
  return false;
}

pub fn is_path_sep(ch: i32) -> bool {
  if ch == 47 { return true; } // '/'
  if ch == 92 { return true; } // '\\'
  return false;
}

pub fn is_abs_path(p: String) -> bool {
  if p.len() == 0 { return false; }
  if is_path_sep(p.byte_at(0)) { return true; }
  if p.len() >= 2 && is_alpha_ascii(p.byte_at(0)) && p.byte_at(1) == 58 { return true; } // C:
  return false;
}

pub fn base_name(p: String) -> String {
  let mut i: i32 = p.len() - 1;
  while i >= 0 {
    if is_path_sep(p.byte_at(i)) {
      return p.slice(i + 1, p.len());
    }
    i = i - 1;
  }
  return p;
}

pub fn dirname(p: String) -> String {
  let n: i32 = p.len();
  let mut i: i32 = n - 1;
  while i >= 0 {
    if is_path_sep(p.byte_at(i)) {
      if i == 0 { return "/"; }
      return p.slice(0, i);
    }
    i = i - 1;
  }
  return ".";
}

pub fn to_forward_slashes(p0: String) -> String {
  let mut out: String = "";
  let mut i: i32 = 0;
  while i < p0.len() {
    if p0.byte_at(i) == 92 { // '\'
      out = out.concat("/");
    } else {
      out = out.concat(p0.slice(i, i + 1));
    }
    i = i + 1;
  }
  return out;
}

pub fn to_back_slashes(p0: String) -> String {
  let mut out: String = "";
  let mut i: i32 = 0;
  while i < p0.len() {
    if p0.byte_at(i) == 47 { // '/'
      out = out.concat("\\");
    } else {
      out = out.concat(p0.slice(i, i + 1));
    }
    i = i + 1;
  }
  return out;
}

pub fn join(a: String, b: String) -> String {
  if a == "" { return b; }
  if b == "" { return a; }
  if is_path_sep(a.byte_at(a.len() - 1)) { return a.concat(b); }
  return a.concat("/").concat(b);
}

pub fn join_slash(a: String, b: String) -> String {
  if a == "" { return b; }
  if b == "" { return a; }
  let an: String = to_forward_slashes(a);
  if an.byte_at(an.len() - 1) == 47 { return an.concat(b); }
  return an.concat("/").concat(b);
}

pub fn dirname_slash(p0: String) -> String {
  let p: String = to_forward_slashes(p0);
  if p == "" { return "."; }
  let mut end: i32 = p.len();
  while end > 1 && p.byte_at(end - 1) == 47 {
    end = end - 1;
  }
  let s: String = p.slice(0, end);
  let mut i: i32 = s.len() - 1;
  while i >= 0 {
    if s.byte_at(i) == 47 {
      if i == 0 { return "/"; }
      if i == 2 && s.len() >= 3 && is_alpha_ascii(s.byte_at(0)) && s.byte_at(1) == 58 && s.byte_at(2) == 47 {
        return s.slice(0, 3);
      }
      return s.slice(0, i);
    }
    i = i - 1;
  }
  return ".";
}

pub fn normalize_msys_drive_path(p0: String) -> String {
  if p0.len() >= 3 && p0.byte_at(0) == 47 && p0.byte_at(2) == 47 && is_alpha_ascii(p0.byte_at(1)) {
    return p0.slice(1, 2).concat(":").concat(p0.slice(2, p0.len()));
  }
  return p0;
}
