// internal string-set/order helpers shared by compiler modules.

import "vox/internal/text" as txt

pub fn contains(xs: Vec[String], x: String) -> bool {
  return txt.contains_str(xs, x);
}

fn contains_sorted(xs: Vec[String], x: String) -> bool {
  let mut lo: i32 = 0;
  let mut hi: i32 = xs.len();
  while lo < hi {
    let mid: i32 = lo + ((hi - lo) / 2);
    let cur: String = xs.get(mid);
    if cur == x { return true; }
    if cur < x {
      lo = mid + 1;
    } else {
      hi = mid;
    }
  }
  return false;
}

pub fn insert_sorted(xs0: Vec[String], x: String) -> Vec[String] {
  let mut lo: i32 = 0;
  let mut hi: i32 = xs0.len();
  while lo < hi {
    let mid: i32 = lo + ((hi - lo) / 2);
    if x < xs0.get(mid) {
      hi = mid;
    } else {
      lo = mid + 1;
    }
  }

  let mut out: Vec[String] = Vec();
  let mut i: i32 = 0;
  while i < lo {
    out.push(xs0.get(i));
    i = i + 1;
  }
  out.push(x);
  while i < xs0.len() {
    out.push(xs0.get(i));
    i = i + 1;
  }
  return out;
}

fn merge_sorted(left: Vec[String], right: Vec[String]) -> Vec[String] {
  let mut out: Vec[String] = Vec();
  let mut li: i32 = 0;
  let mut ri: i32 = 0;
  while li < left.len() && ri < right.len() {
    let l: String = left.get(li);
    let r: String = right.get(ri);
    if l <= r {
      out.push(l);
      li = li + 1;
    } else {
      out.push(r);
      ri = ri + 1;
    }
  }
  while li < left.len() {
    out.push(left.get(li));
    li = li + 1;
  }
  while ri < right.len() {
    out.push(right.get(ri));
    ri = ri + 1;
  }
  return out;
}

pub fn sort(xs0: Vec[String]) -> Vec[String] {
  if xs0.len() <= 1 { return xs0; }
  let mid: i32 = xs0.len() / 2;
  let mut left: Vec[String] = Vec();
  let mut right: Vec[String] = Vec();
  let mut i: i32 = 0;
  while i < xs0.len() {
    if i < mid {
      left.push(xs0.get(i));
    } else {
      right.push(xs0.get(i));
    }
    i = i + 1;
  }
  return merge_sorted(sort(left), sort(right));
}

pub fn push_unique_sorted(xs0: Vec[String], x: String) -> Vec[String] {
  let ys: Vec[String] = sort(xs0);
  if contains_sorted(ys, x) { return ys; }
  return insert_sorted(ys, x);
}
