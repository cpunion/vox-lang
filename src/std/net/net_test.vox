import "std/testing" as t
import "std/net" as net

fn test_url_parse_and_format_smoke() -> () {
  let u: net.Url = net.parse_url("https://api.example.com:8443/v1/chat?model=vox");
  t.assert_eq(u.scheme, "https");
  t.assert_eq(u.host, "api.example.com");
  t.assert_eq(u.port, 8443);
  t.assert_eq(u.path, "/v1/chat");
  t.assert_eq(u.query, "model=vox");

  t.assert_eq(net.url_to_string(u), "https://api.example.com:8443/v1/chat?model=vox");
}

fn test_url_parse_default_port_and_query_only_smoke() -> () {
  let u0: net.Url = net.parse_url("http://a.io");
  t.assert_eq(u0.port, 80);
  t.assert_eq(u0.path, "/");

  let u1: net.Url = net.parse_url("https://a.io?x=1");
  t.assert_eq(u1.port, 443);
  t.assert_eq(u1.path, "/");
  t.assert_eq(u1.query, "x=1");
}

fn test_query_escape_and_build_smoke() -> () {
  t.assert_eq(net.query_escape("vox lang"), "vox+lang");

  let mut qs: Vec[net.QueryItem] = Vec();
  qs.push(net.query_item("q", "vox lang"));
  qs.push(net.query_item("page", "1"));
  t.assert_eq(net.build_query(qs), "q=vox+lang&page=1");
}

fn test_http_request_render_smoke() -> () {
  let mut req: net.HttpRequest = net.http_request("POST", "example.com", "/v1/run");
  req = req.with_header("Content-Type", "application/json");
  req = req.with_body("{\"ok\":true}");

  let text: String = req.render();
  t.assert(net.parse_status_code("HTTP/1.1 200 OK\r\n\r\n") == 200);
  t.assert(net.header_value(text, "Host") == "example.com");
  t.assert(net.header_value(text, "Content-Length") == "11");
}

fn test_http_response_helpers_smoke() -> () {
  let raw: String = "HTTP/1.1 404 Not Found\r\nContent-Type: text/plain\r\n\r\nmissing";
  let st: net.HttpResponseHead = net.parse_status(raw);
  t.assert_eq(st.code, 404);
  t.assert_eq(st.reason, "Not Found");
  t.assert_eq(net.response_body(raw), "missing");
  t.assert_eq(net.header_value(raw, "Content-Type"), "text/plain");
}
