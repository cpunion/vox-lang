import "std/testing" as t
import "std/collections" as c

fn test_set_add_contains_len_smoke() -> () {
  let mut s: c.Set[String] = c.set();
  t.assert(s.is_empty());

  s = s.add("a");
  s = s.add("a");
  s = s.add("b");

  t.assert_eq(s.len(), 2);
  t.assert(s.contains("a"));
  t.assert(s.contains("b"));
  t.assert(!s.contains("c"));
}

fn test_set_remove_and_clear_smoke() -> () {
  let mut s: c.Set[i32] = c.set();
  s = s.add(1);
  s = s.add(2);

  s = s.remove(1);
  t.assert_eq(s.len(), 1);
  t.assert(!s.contains(1));
  t.assert(s.contains(2));

  s = s.clear();
  t.assert(s.is_empty());
}

fn test_set_values_smoke() -> () {
  let mut a: c.Set[i32] = c.set();
  a = a.add(1);
  a = a.add(2);
  let xs: Vec[i32] = Clone.clone(a).values();
  t.assert_eq(xs.len(), 2);
  t.assert(a.contains(xs.get(0)));
  t.assert(a.contains(xs.get(1)));
}

fn test_set_clone_and_release_alias_safe_smoke() -> () {
  let mut s: c.Set[String] = c.set();
  s = s.add("vox");
  s = s.add("lang");

  let mut cpy: c.Set[String] = Clone.clone(s);
  cpy = cpy.remove("vox");

  t.assert(s.contains("vox"));
  t.assert(!cpy.contains("vox"));

  let keep: c.Set[String] = s;
  s = s.release();
  t.assert(s.is_empty());
  t.assert(keep.contains("vox"));
  t.assert(keep.contains("lang"));
}
