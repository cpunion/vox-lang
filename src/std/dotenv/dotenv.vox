import "std/fs" as fs
import "std/string" as s

fn trim_left_ws(raw: String) -> String {
  let mut i: i32 = 0;
  while i < raw.len() {
    let ch: i32 = raw.byte_at(i);
    if ch == 32 || ch == 9 {
      i = i + 1;
    } else {
      return raw.slice(i, raw.len());
    }
  }
  return "";
}

fn trim_ws(raw: String) -> String {
  let left: String = trim_left_ws(raw);
  if left.len() == 0 { return ""; }

  let mut hi: i32 = left.len();
  while hi > 0 {
    let ch: i32 = left.byte_at(hi - 1);
    if ch == 32 || ch == 9 || ch == 10 || ch == 13 {
      hi = hi - 1;
    } else {
      break;
    }
  }
  return left.slice(0, hi);
}

fn line_end(text: String, start: i32) -> i32 {
  if start >= text.len() { return text.len(); }
  let rest: String = text.slice(start, text.len());
  let lf: i32 = s.index_of(s.view_all(rest), "\n");
  if lf < 0 { return text.len(); }
  return start + lf;
}

fn unquote(raw: String) -> String {
  let t: String = trim_ws(raw);
  if t.len() >= 2 && t.byte_at(0) == 34 && t.byte_at(t.len() - 1) == 34 {
    return t.slice(1, t.len() - 1);
  }
  if t.len() >= 2 && t.byte_at(0) == 39 && t.byte_at(t.len() - 1) == 39 {
    return t.slice(1, t.len() - 1);
  }
  return t;
}

pub fn value(path: String, key: String) -> String {
  if !fs.exists(path) { return ""; }

  let text: String = fs.read_to_string(path);
  let mut start: i32 = 0;
  while start < text.len() {
    let end: i32 = line_end(text, start);
    let mut line: String = trim_ws(text.slice(start, end));
    if line.len() > 0 && line.byte_at(0) != 35 {
      if s.starts_with(s.view_all(line), "export ") {
        line = trim_left_ws(line.slice(7, line.len()));
      }
      let sep: i32 = s.index_of(s.view_all(line), "=");
      if sep > 0 {
        let lhs: String = trim_ws(line.slice(0, sep));
        if lhs == key {
          let rhs: String = trim_left_ws(line.slice(sep + 1, line.len()));
          return unquote(rhs);
        }
      }
    }
    start = end + 1;
  }

  return "";
}

pub fn value_from_paths(paths: Vec[String], key: String) -> String {
  let mut i: i32 = 0;
  while i < paths.len() {
    let v: String = value(paths.get(i), key);
    if v != "" { return v; }
    i = i + 1;
  }
  return "";
}
