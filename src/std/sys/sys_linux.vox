@build(linux)
@ffi_import("c", "read")
fn c_read(fd: i32, buf: rawptr, n: usize) -> isize;

@ffi_import("c", "open")
fn c_open(path: String, flags: i32) -> i32;

pub fn open_read(path: String) -> i32 {
  // O_RDONLY = 0 on POSIX.
  return c_open(path, 0);
}

pub fn read(fd: i32, buf: rawptr, n: usize) -> isize {
  return c_read(fd, buf, n);
}

@ffi_import("c", "write")
fn c_write(fd: i32, buf: const rawptr, n: usize) -> isize;

pub fn write(fd: i32, buf: const rawptr, n: usize) -> isize {
  return c_write(fd, buf, n);
}

@ffi_import("c", "sched_yield")
fn c_sched_yield() -> i32;

pub fn yield_now() -> () {
  let _rc: i32 = c_sched_yield();
  _rc;
  return;
}

@ffi_import("c", "send")
fn c_send(fd: i32, buf: const rawptr, n: usize, flags: i32) -> isize;

fn handle_to_fd(h: isize) -> i32 {
  if h < -2147483648 as isize || h > 2147483647 as isize {
    panic("sys.socket_send handle out of i32 range");
  }
  return h as i32;
}

pub fn socket_send(handle: isize, buf: String, n: usize) -> isize {
  let fd: i32 = handle_to_fd(handle);
  return c_send(fd, buf as const rawptr, n, 0);
}

@ffi_import("c", "recv")
fn c_recv_raw(fd: i32, buf: rawptr, n: usize, flags: i32) -> isize;

pub fn connect(host: String, port: i32) -> isize {
  if port <= 0 || port > 65535 { panic("invalid tcp port"); }
  let port_str: String = port.to_string();
  // addrinfo hints: ai_family=AF_UNSPEC(0), ai_socktype=SOCK_STREAM
  let hints: rawptr = calloc(1 as usize, ADDRINFO_SIZE() as usize);
  @ptr_write[i32](@ptr_offset(hints, ADDRINFO_OFF_SOCKTYPE()), SOCK_STREAM());
  // resolve
  let res_buf: rawptr = calloc(1 as usize, 8 as usize);
  let rc: i32 = sock_getaddrinfo(host, port_str, hints, res_buf);
  free(hints);
  if rc != 0 {
    free(res_buf);
    panic("tcp connect resolve failed");
  }
  let res: rawptr = @ptr_read[rawptr](res_buf);
  // loop through results
  let mut fd: i32 = -1;
  let mut p: rawptr = res;
  while (p as isize) != 0 as isize && fd < 0 {
    let family: i32 = @ptr_read[i32](@ptr_offset(p, ADDRINFO_OFF_FAMILY()));
    let socktype: i32 = @ptr_read[i32](@ptr_offset(p, ADDRINFO_OFF_SOCKTYPE()));
    let protocol: i32 = @ptr_read[i32](@ptr_offset(p, ADDRINFO_OFF_PROTOCOL()));
    let sfd: i32 = sock_socket(family, socktype, protocol);
    if sfd >= 0 {
      let addr: rawptr = @ptr_read[rawptr](@ptr_offset(p, ADDRINFO_OFF_ADDR()));
      let addrlen: u32 = @ptr_read[u32](@ptr_offset(p, ADDRINFO_OFF_ADDRLEN()));
      if sock_connect(sfd, addr, addrlen) == 0 {
        fd = sfd;
      } else {
        let _cr: i32 = c_close(sfd);
        _cr;
      }
    }
    if fd < 0 {
      p = @ptr_read[rawptr](@ptr_offset(p, ADDRINFO_OFF_NEXT()));
    }
  }
  sock_freeaddrinfo(res);
  free(res_buf);
  if fd < 0 { panic("tcp connect failed"); }
  return fd as isize;
}

pub fn recv(handle: isize, max_n: i32) -> String {
  let fd: i32 = handle_to_fd(handle);
  if fd < 0 { panic("invalid tcp handle"); }
  if max_n <= 0 { return ""; }
  let buf: rawptr = calloc(1 as usize, (max_n + 1) as usize);
  let n: isize = c_recv_raw(fd, buf, max_n as usize, 0);
  if n < 0 as isize { panic("tcp recv failed"); }
  @ptr_write[u8](@ptr_offset(buf, n), 0 as u8);
  return buf as String;
}

pub fn close_socket(handle: isize) -> () {
  if handle < 0 { return; }
  let _rc: i32 = c_close(handle_to_fd(handle));
  _rc;
  return;
}

pub fn wait_read(handle: isize, timeout_ms: i32) -> bool {
  let fd: i32 = handle_to_fd(handle);
  if fd < 0 { panic("invalid tcp handle"); }
  return tcp_wait_epoll(fd, false, if timeout_ms < 0 { 0 } else { timeout_ms });
}

pub fn wait_write(handle: isize, timeout_ms: i32) -> bool {
  let fd: i32 = handle_to_fd(handle);
  if fd < 0 { panic("invalid tcp handle"); }
  return tcp_wait_epoll(fd, true, if timeout_ms < 0 { 0 } else { timeout_ms });
}

fn tcp_wait_epoll(fd: i32, want_write: bool, timeout_ms: i32) -> bool {
  let ep: i32 = epoll_create();
  if ep < 0 { panic("tcp epoll create failed"); }
  let events: u32 = if want_write {
    EPOLLOUT() | EPOLLERR() | EPOLLHUP()
  } else {
    EPOLLIN() | EPOLLERR() | EPOLLHUP()
  };
  let ev: rawptr = calloc(1 as usize, EPOLL_EVENT_SIZE() as usize);
  epoll_event_set(ev, 0, events, 1 as u64);
  if epoll_ctl(ep, EPOLL_CTL_ADD(), fd, ev) != 0 {
    free(ev);
    close_fd(ep);
    panic("tcp epoll ctl failed");
  }
  let n: i32 = epoll_wait_events(ep, ev, 1, timeout_ms);
  free(ev);
  close_fd(ep);
  return n > 0;
}

@ffi_import("c", "close")
fn c_close(fd: i32) -> i32;

pub fn close(fd: i32) -> i32 {
  return c_close(fd);
}

@ffi_import("c", "lseek")
fn c_lseek(fd: i32, offset: i64, whence: i32) -> i64;

pub fn lseek(fd: i32, offset: i64, whence: i32) -> i64 {
  return c_lseek(fd, offset, whence);
}

@ffi_import("c", "access")
fn c_access(path: String, mode: i32) -> i32;

pub fn access(path: String, mode: i32) -> i32 {
  return c_access(path, mode);
}

@ffi_import("c", "mkdir")
fn c_mkdir(path: String, mode: u32) -> i32;

pub fn mkdir(path: String, mode: u16) -> i32 {
  return c_mkdir(path, mode as u32);
}

@ffi_import("c", "creat")
fn c_creat(path: String, mode: u32) -> i32;

pub fn creat(path: String, mode: u16) -> i32 {
  return c_creat(path, mode as u32);
}

// CLOCK_MONOTONIC = 1 on Linux.
// Thin wrapper avoids clock_gettime extern conflict with system headers.
@ffi_import("c", "vox_impl_clock_gettime")
fn c_clock_gettime(clk_id: i32, tp: rawptr) -> i32;

pub fn now_ns() -> i64 {
  let buf: rawptr = calloc(1 as usize, 16 as usize);
  let rc: i32 = c_clock_gettime(1, buf);
  if rc != 0 {
    free(buf);
    return 0 as i64;
  }
  let sec: i64 = @ptr_read[i64](buf);
  let nsec: i64 = @ptr_read[i64](@ptr_offset(buf, 8 as isize));
  free(buf);
  return sec * 1000000000 as i64 + nsec;
}
