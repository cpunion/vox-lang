@build(windows && amd64)
@ffi_import("c", "_read")
fn c_read(fd: i32, buf: rawptr, n: u32) -> i32;

@ffi_import("c", "_sopen_s")
fn c_sopen_s(fd_out: &mut i32, path: String, flags: i32, shflag: i32, mode: i32) -> i32;

pub fn open_read(path: String) -> i32 {
  // `_sopen_s` avoids varargs and works with x86 CRT calling convention.
  // - flags O_RDONLY = 0
  // - share _SH_DENYNO = 0x40
  let mut fd: i32 = -1;
  let rc: i32 = c_sopen_s(fd, path, 0, 64, 0);
  if rc != 0 { return -1; }
  return fd;
}

@ffi_import("c", "_write")
fn c_write(fd: i32, buf: const rawptr, n: u32) -> i32;

fn count_to_u32(n: usize) -> u32 {
  if n > 4294967295 as usize { panic("sys count too large for windows"); }
  return n as u32;
}

pub fn read(fd: i32, buf: rawptr, n: usize) -> isize {
  return c_read(fd, buf, count_to_u32(n)) as isize;
}

pub fn write(fd: i32, buf: const rawptr, n: usize) -> isize {
  return c_write(fd, buf, count_to_u32(n)) as isize;
}

@ffi_import("c", "usleep")
fn c_usleep(usec: u32) -> i32;

pub fn yield_now() -> () {
  let _rc: i32 = c_usleep(0 as u32);
  _rc;
  return;
}

@calling_convention("system")
@ffi_import("c", "send")
fn c_send(sock: usize, buf: String, n: i32, flags: i32) -> i32;

fn count_to_i32(n: usize) -> i32 {
  if n > 2147483647 as usize { panic("sys count too large for windows send"); }
  return n as i32;
}

pub fn socket_send(handle: isize, buf: String, n: usize) -> isize {
  if handle < 0 { return -1 as isize; }
  return c_send(handle as usize, buf, count_to_i32(n), 0) as isize;
}

@calling_convention("system")
@ffi_import("c", "WSAStartup") fn win_WSAStartup(ver: u16, data: rawptr) -> i32;
@calling_convention("system")
@ffi_import("c", "recv") fn win_recv(fd: usize, buf: rawptr, len: i32, flags: i32) -> i32;
@calling_convention("system")
@ffi_import("c", "closesocket") fn win_closesocket(fd: usize) -> i32;
@calling_convention("system")
@ffi_import("c", "select") fn win_select(nfds: i32, readfds: rawptr, writefds: rawptr, exceptfds: rawptr, timeout: rawptr) -> i32;

static mut wsa_inited: bool = false;
fn wsa_init() -> () {
  if wsa_inited { return; }
  let wsa: rawptr = calloc(1 as usize, 408 as usize);
  let rc: i32 = win_WSAStartup(514 as u16, wsa);
  free(wsa);
  if rc != 0 { panic("wsa startup failed"); }
  wsa_inited = true;
  return;
}

fn win_sock_poll(fd: i32, want_write: bool, timeout_ms: i32) -> bool {
  let mut tms: i32 = timeout_ms;
  if tms < 0 { tms = 0; }
  // fd_set: 520 bytes (count u32 at offset 0, SOCKET at offset 8)
  let fds: rawptr = calloc(1 as usize, 520 as usize);
  @ptr_write[u32](fds, 1 as u32);
  @ptr_write[usize](@ptr_offset(fds, 8 as isize), fd as usize);
  // timeval: tv_sec (i32) + tv_usec (i32) = 8 bytes
  let tv: rawptr = calloc(1 as usize, 8 as usize);
  @ptr_write[i32](tv, tms / 1000);
  @ptr_write[i32](@ptr_offset(tv, 4 as isize), (tms % 1000) * 1000);
  let mut rfds: rawptr = fds;
  let mut wfds: rawptr = null_rawptr();
  if want_write {
    rfds = null_rawptr();
    wfds = fds;
  }
  let n: i32 = win_select(0, rfds, wfds, null_rawptr(), tv);
  free(fds);
  free(tv);
  return n > 0;
}

pub fn connect(host: String, port: i32) -> isize {
  wsa_init();
  if port <= 0 || port > 65535 { panic("invalid tcp port"); }
  let port_str: String = port.to_string();
  let hints: rawptr = calloc(1 as usize, ADDRINFO_SIZE() as usize);
  @ptr_write[i32](@ptr_offset(hints, ADDRINFO_OFF_SOCKTYPE()), SOCK_STREAM());
  @ptr_write[i32](@ptr_offset(hints, ADDRINFO_OFF_PROTOCOL()), IPPROTO_TCP());
  let res_buf: rawptr = calloc(1 as usize, 8 as usize);
  let rc: i32 = sock_getaddrinfo(host, port_str, hints, res_buf);
  free(hints);
  if rc != 0 {
    free(res_buf);
    panic("tcp connect resolve failed");
  }
  let res: isize = @ptr_read[isize](res_buf);
  free(res_buf);
  if res == (0 as isize) { panic("tcp connect resolve failed"); }
  let mut fd: i32 = -1;
  let mut p: isize = res;
  while p != (0 as isize) {
    let pp: rawptr = p as rawptr;
    let family: i32 = @ptr_read[i32](@ptr_offset(pp, ADDRINFO_OFF_FAMILY()));
    let socktype: i32 = @ptr_read[i32](@ptr_offset(pp, ADDRINFO_OFF_SOCKTYPE()));
    let protocol: i32 = @ptr_read[i32](@ptr_offset(pp, ADDRINFO_OFF_PROTOCOL()));
    let addrlen: u32 = @ptr_read[u32](@ptr_offset(pp, ADDRINFO_OFF_ADDRLEN()));
    let addr: rawptr = (@ptr_read[isize](@ptr_offset(pp, ADDRINFO_OFF_ADDR()))) as rawptr;
    fd = sock_socket(family, socktype, protocol);
    if fd >= 0 {
      if sock_connect(fd, addr, addrlen) == 0 {
        sock_freeaddrinfo(res as rawptr);
        return fd as isize;
      }
      let _r: i32 = win_closesocket(fd as usize);
      _r;
      fd = -1;
    }
    p = @ptr_read[isize](@ptr_offset(pp, ADDRINFO_OFF_NEXT()));
  }
  sock_freeaddrinfo(res as rawptr);
  panic("tcp connect failed");
  return -1 as isize;
}

pub fn recv(handle: isize, max_n: i32) -> String {
  if handle < 0 { panic("invalid tcp handle"); }
  if max_n <= 0 { return ""; }
  let buf: rawptr = calloc(1 as usize, (max_n as usize) + (1 as usize));
  let n: i32 = win_recv(handle as usize, buf, max_n, 0);
  if n < 0 { panic("tcp recv failed"); }
  return buf_to_string(buf, n);
}

pub fn close_socket(handle: isize) -> () {
  if handle < 0 { return; }
  let _rc: i32 = win_closesocket(handle as usize);
  _rc;
  return;
}

pub fn wait_read(handle: isize, timeout_ms: i32) -> bool {
  if handle < 0 { panic("invalid tcp handle"); }
  return win_sock_poll(handle as i32, false, timeout_ms);
}

pub fn wait_write(handle: isize, timeout_ms: i32) -> bool {
  if handle < 0 { panic("invalid tcp handle"); }
  return win_sock_poll(handle as i32, true, timeout_ms);
}

@ffi_import("c", "_lseeki64")
fn c_lseek(fd: i32, offset: i64, whence: i32) -> i64;

pub fn lseek(fd: i32, offset: i64, whence: i32) -> i64 {
  return c_lseek(fd, offset, whence);
}

@ffi_import("c", "_close")
fn c_close(fd: i32) -> i32;

pub fn close(fd: i32) -> i32 {
  return c_close(fd);
}

@ffi_import("c", "_access")
fn c_access(path: String, mode: i32) -> i32;

pub fn access(path: String, mode: i32) -> i32 {
  return c_access(path, mode);
}

@ffi_import("c", "_mkdir")
fn c_mkdir(path: String) -> i32;

pub fn mkdir(path: String, _mode: u16) -> i32 {
  return c_mkdir(path);
}

@ffi_import("c", "_creat")
fn c_creat(path: String, mode: i32) -> i32;

pub fn creat(path: String, mode: u16) -> i32 {
  // Windows CRT creat only honors owner read/write bits.
  let mut pmode: i32 = mode as i32;
  if pmode > 384 { pmode = 384; }
  if pmode <= 0 { pmode = 384; }
  return c_creat(path, pmode);
}
