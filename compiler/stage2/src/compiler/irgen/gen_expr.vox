import "compiler/ast" as ast
import "compiler/ir" as ir
import "compiler/typecheck" as tc

struct GenExprResult { ok: bool, err: String, g: Gen, val: ir.Value, ty: i32 }
struct TryEnumInfo { ok: bool, is_result: bool, ok_name: String, ok_idx: i32, ok_ty: i32, residual_name: String, residual_idx: i32, residual_ty: i32 }
struct CurrentTryResult { found: bool, t: TryCtx }
struct TryErrConvResult { ok: bool, err: String, need_call: bool, mod_path: String, fn_name: String, ret_ty: i32, g: Gen }

fn gen_expr(g0: Gen, id: i32, expected: i32) -> GenExprResult {
  let tr: tc.ExprTyResult = tc.tc_expr(g0.ctx, g0.w, g0.mod_path, g0.imps, g0.exprs, g0.locals, id, expected);
  if !tr.ok {
    return GenExprResult { ok: false, err: tc.tc_error_to_string(tr.err), g: g0, val: ir.v_int(0), ty: g0.ctx.ty_bad };
  }

  let n: ast.ExprNode = ast.expr_pool_get(g0.exprs, id);
  return match n {
    ast.ExprNode.Int(v) => gen_expr_int(g0, v, tr.ty, expected),
    ast.ExprNode.Float(v) => gen_expr_float(g0, v, tr.ty, expected),
    ast.ExprNode.Bool(b) => gen_expr_bool(g0, b, tr.ty),
    ast.ExprNode.Str(s) => gen_expr_str(g0, s, tr.ty),
    ast.ExprNode.Ident(name) => gen_expr_ident(g0, name, tr.ty),
    ast.ExprNode.DotIdent(name) => gen_expr_dot_ident(g0, name, tr.ty),
    ast.ExprNode.Block(b) => gen_expr_block(g0, b, tr.ty),
    ast.ExprNode.Unary(op, a) => gen_expr_unary(g0, op, a, tr.ty),
    ast.ExprNode.As(a, tn) => gen_expr_as(g0, a, tn, tr.ty),
    ast.ExprNode.Binary(op, l, r) => gen_expr_binary(g0, op, l, r, tr.ty),
    ast.ExprNode.If(c, t, e) => gen_expr_if(g0, c, t, e, tr.ty),
    ast.ExprNode.Member(recv, name) => gen_expr_member(g0, recv, name, tr.ty),
    ast.ExprNode.Call(callee, type_args, args) => gen_expr_call(g0, callee, type_args, args, tr.ty, expected),
    ast.ExprNode.MacroCall(_callee, _targs, _args) =>
      GenExprResult { ok: false, err: "internal: unexpanded macro call reached irgen", g: g0, val: ir.v_int(0), ty: g0.ctx.ty_bad },
    ast.ExprNode.TryBlock(bid) => gen_expr_try_block(g0, bid, tr.ty),
    ast.ExprNode.Try(a) => gen_expr_try(g0, a, tr.ty),
    ast.ExprNode.StructLit(ty_expr, fields) => gen_expr_struct_lit(g0, ty_expr, fields, tr.ty),
    ast.ExprNode.Match(scrut, arms) => gen_expr_match(g0, scrut, arms, tr.ty),
  };
}

fn current_try(g0: Gen) -> CurrentTryResult {
  if g0.tries.len() == 0 { return CurrentTryResult { found: false, t: TryCtx { ret_slot: -1, end_blk: "", ret_ty: -1 } }; }
  return CurrentTryResult { found: true, t: g0.tries.get(g0.tries.len() - 1) };
}

fn try_stack_pop(v: Vec[TryCtx]) -> Vec[TryCtx] {
  if v.len() == 0 { return v; }
  let mut out: Vec[TryCtx] = Vec();
  let mut i: i32 = 0;
  while i < v.len() - 1 {
    out.push(v.get(i));
    i = i + 1;
  }
  return out;
}

fn find_variant_index(es: tc.EnumSig, name: String) -> i32 {
  let mut i: i32 = 0;
  while i < es.vars.len() {
    if es.vars.get(i).name == name { return i; }
    i = i + 1;
  }
  return -1;
}

fn try_enum_info(g0: Gen, ty0: i32) -> TryEnumInfo {
  let ty: i32 = tc.strip_range(g0.ctx, ty0);
  if ty < 0 || ty >= g0.ctx.pool.tys.len() {
    return TryEnumInfo { ok: false, is_result: false, ok_name: "", ok_idx: -1, ok_ty: g0.ctx.ty_bad, residual_name: "", residual_idx: -1, residual_ty: g0.ctx.ty_bad };
  }
  let t: ir.Ty = ir.ty_pool_get(g0.ctx.pool, ty);
  if t.kind != ir.TyKind.Enum {
    return TryEnumInfo { ok: false, is_result: false, ok_name: "", ok_idx: -1, ok_ty: g0.ctx.ty_bad, residual_name: "", residual_idx: -1, residual_ty: g0.ctx.ty_bad };
  }
  let fe: tc.FindEnumResult = tc.find_enum(g0.ctx, tc.ty_mod(t), tc.ty_name(t));
  if !fe.found {
    return TryEnumInfo { ok: false, is_result: false, ok_name: "", ok_idx: -1, ok_ty: g0.ctx.ty_bad, residual_name: "", residual_idx: -1, residual_ty: g0.ctx.ty_bad };
  }
  let es: tc.EnumSig = fe.sig;

  let ok_idx: i32 = find_variant_index(es, "Ok");
  let err_idx: i32 = find_variant_index(es, "Err");
  if ok_idx != -1 && err_idx != -1 {
    let okv: tc.EnumVariantSig = es.vars.get(ok_idx);
    let errv: tc.EnumVariantSig = es.vars.get(err_idx);
    if okv.fields.len() == 1 && errv.fields.len() == 1 {
      return TryEnumInfo {
        ok: true,
        is_result: true,
        ok_name: "Ok",
        ok_idx: ok_idx,
        ok_ty: okv.fields.get(0),
        residual_name: "Err",
        residual_idx: err_idx,
        residual_ty: errv.fields.get(0),
      };
    }
  }

  let some_idx: i32 = find_variant_index(es, "Some");
  let none_idx: i32 = find_variant_index(es, "None");
  if some_idx != -1 && none_idx != -1 {
    let somev: tc.EnumVariantSig = es.vars.get(some_idx);
    let nonev: tc.EnumVariantSig = es.vars.get(none_idx);
    if somev.fields.len() == 1 && nonev.fields.len() == 0 {
      return TryEnumInfo {
        ok: true,
        is_result: false,
        ok_name: "Some",
        ok_idx: some_idx,
        ok_ty: somev.fields.get(0),
        residual_name: "None",
        residual_idx: none_idx,
        residual_ty: g0.ctx.ty_unit,
      };
    }
  }

  return TryEnumInfo { ok: false, is_result: false, ok_name: "", ok_idx: -1, ok_ty: g0.ctx.ty_bad, residual_name: "", residual_idx: -1, residual_ty: g0.ctx.ty_bad };
}

fn try_impl_assoc_ty_ir(sig: tc.ImplSig, assoc: String) -> i32 {
  let mut i: i32 = 0;
  while i < sig.assoc_types.len() {
    let a: tc.ImplAssocTypeSig = sig.assoc_types.get(i);
    if a.name == assoc { return a.ty; }
    i = i + 1;
  }
  return -1;
}

fn try_subs_lookup_ir(subs: Vec[tc.TySub], name: String) -> i32 {
  let mut i: i32 = 0;
  while i < subs.len() {
    let s: tc.TySub = subs.get(i);
    if s.name == name { return s.ty; }
    i = i + 1;
  }
  return -1;
}

fn try_resolve_err_conv(g0: Gen, src_err_ty: i32, dst_err_ty: i32) -> TryErrConvResult {
  if tc.assignable_to(g0.ctx, dst_err_ty, src_err_ty) {
    return TryErrConvResult { ok: true, err: "", need_call: false, mod_path: "", fn_name: "", ret_ty: src_err_ty, g: g0 };
  }

  let prelude: String = "std/prelude";
  let tf: tc.FindTraitResult = tc.find_trait(g0.ctx, prelude, "Into");
  if !tf.found || !tc.vis_allows(tf.sig.vis, prelude, g0.mod_path) {
    return TryErrConvResult { ok: false, err: "cannot propagate error: Err type mismatch", need_call: false, mod_path: "", fn_name: "", ret_ty: g0.ctx.ty_bad, g: g0 };
  }

  let fi: tc.FindImplForTyResult = tc.find_impl_for_ty(g0.ctx, prelude, "Into", tc.strip_range(g0.ctx, src_err_ty));
  if fi.ambiguous {
    let msg0: String = "cannot propagate error: Err type mismatch (ambiguous Into impls: ".concat(fi.candidates).concat(")");
    return TryErrConvResult { ok: false, err: msg0, need_call: false, mod_path: "", fn_name: "", ret_ty: g0.ctx.ty_bad, g: g0 };
  }
  if !fi.found {
    return TryErrConvResult { ok: false, err: "cannot propagate error: Err type mismatch", need_call: false, mod_path: "", fn_name: "", ret_ty: g0.ctx.ty_bad, g: g0 };
  }

  let im: tc.FindImplMethodResult = tc.find_impl_method(fi.sig, "into");
  if !im.found {
    return TryErrConvResult { ok: false, err: "cannot propagate error: Err type mismatch", need_call: false, mod_path: "", fn_name: "", ret_ty: g0.ctx.ty_bad, g: g0 };
  }
  if im.m.sig.params.len() != 1 {
    return TryErrConvResult { ok: false, err: "cannot propagate error: Err type mismatch", need_call: false, mod_path: "", fn_name: "", ret_ty: g0.ctx.ty_bad, g: g0 };
  }
  if im.m.sig.const_params.len() != 0 {
    return TryErrConvResult { ok: false, err: "cannot propagate error: Err type mismatch", need_call: false, mod_path: "", fn_name: "", ret_ty: g0.ctx.ty_bad, g: g0 };
  }

  let mut subs: Vec[tc.TySub] = Vec();
  let ur0: tc.UnifyResult = tc.unify_ty(g0.ctx, im.m.sig.ret, dst_err_ty, subs);
  if !ur0.ok {
    return TryErrConvResult { ok: false, err: "cannot propagate error: Err type mismatch", need_call: false, mod_path: "", fn_name: "", ret_ty: g0.ctx.ty_bad, g: g0 };
  }
  subs = ur0.subs;
  let ur1: tc.UnifyResult = tc.unify_ty(g0.ctx, im.m.sig.params.get(0), src_err_ty, subs);
  if !ur1.ok {
    return TryErrConvResult { ok: false, err: "cannot propagate error: Err type mismatch", need_call: false, mod_path: "", fn_name: "", ret_ty: g0.ctx.ty_bad, g: g0 };
  }
  subs = ur1.subs;
  let mut ti: i32 = 0;
  while ti < im.m.sig.type_params.len() {
    let tp: String = im.m.sig.type_params.get(ti);
    if try_subs_lookup_ir(subs, tp) == -1 {
      subs.push(tc.TySub { name: tp, ty: dst_err_ty });
    }
    ti = ti + 1;
  }
  let mut bi: i32 = 0;
  while bi < im.m.sig.type_param_bounds.len() {
    let b: tc.TypeParamBoundSig = im.m.sig.type_param_bounds.get(bi);
    let ty0: i32 = try_subs_lookup_ir(subs, b.type_param);
    if ty0 == -1 {
      return TryErrConvResult { ok: false, err: "cannot propagate error: Err type mismatch", need_call: false, mod_path: "", fn_name: "", ret_ty: g0.ctx.ty_bad, g: g0 };
    }
    let fi2: tc.FindImplForTyResult = tc.find_impl_for_ty(g0.ctx, b.trait_mod_path, b.trait_name, tc.strip_range(g0.ctx, ty0));
    if fi2.ambiguous || !fi2.found {
      return TryErrConvResult { ok: false, err: "cannot propagate error: Err type mismatch", need_call: false, mod_path: "", fn_name: "", ret_ty: g0.ctx.ty_bad, g: g0 };
    }
    bi = bi + 1;
  }

  let assoc_target: i32 = try_impl_assoc_ty_ir(fi.sig, "Target");
  let via_ty0: i32 = if assoc_target != -1 { assoc_target } else { im.m.sig.ret };
  let via_ty: i32 = tc.subst_ty(g0.ctx, via_ty0, subs);
  if !tc.assignable_to(g0.ctx, dst_err_ty, via_ty) {
    return TryErrConvResult { ok: false, err: "cannot propagate error: Err type mismatch", need_call: false, mod_path: "", fn_name: "", ret_ty: g0.ctx.ty_bad, g: g0 };
  }

  if im.m.sig.type_params.len() == 0 {
    return TryErrConvResult {
      ok: true,
      err: "",
      need_call: true,
      mod_path: fi.sig.mod_path,
      fn_name: im.m.lowered_name,
      ret_ty: via_ty,
      g: g0,
    };
  }

  let mut ps: Vec[i32] = Vec();
  ps.push(tc.subst_ty(g0.ctx, im.m.sig.params.get(0), subs));
  let inst_sig: tc.FuncSig = tc.FuncSig {
    params: ps,
    param_borrow_kinds: im.m.sig.param_borrow_kinds,
    ret: via_ty,
    has_variadic: false,
    variadic_elem: -1,
    vis: im.m.sig.vis,
    is_pub: im.m.sig.is_pub,
    type_params: Vec(),
    type_param_packs: Vec(),
    const_params: im.m.sig.const_params,
    type_param_bounds: Vec(),
    const_where_bounds: im.m.sig.const_where_bounds,
  };
  let inst: String = tc.inst_name_ex(g0.ctx, im.m.lowered_name, im.m.sig.type_params, im.m.sig.const_params, subs, Vec());
  let inst_qn: String = qname(fi.sig.mod_path, inst);
  let mut g1: Gen = g0;
  g1.pending = pending_add(g1.pending, PendingInst {
    qname: inst_qn,
    mod_path: fi.sig.mod_path,
    base_name: im.m.lowered_name,
    inst_name: inst,
    sig: inst_sig,
    subs: subs,
    const_subs: Vec(),
  });

  return TryErrConvResult {
    ok: true,
    err: "",
    need_call: true,
    mod_path: fi.sig.mod_path,
    fn_name: inst,
    ret_ty: via_ty,
    g: g1,
  };
}

fn gen_expr_try(g0: Gen, a: i32, out_ty: i32) -> GenExprResult {
  let mut g: Gen = g0;
  let ar: GenExprResult = gen_expr(g, a, -1);
  if !ar.ok { return ar; }
  g = ar.g;

  let src: TryEnumInfo = try_enum_info(g, ar.ty);
  if !src.ok { return GenExprResult { ok: false, err: "`?` requires Result[T, E] or Option[T]", g: g, val: ir.v_int(0), ty: g.ctx.ty_bad }; }
  let ct: CurrentTryResult = current_try(g);
  let target_ret_ty: i32 = if ct.found { ct.t.ret_ty } else { g.ret_ty };
  let dst: TryEnumInfo = try_enum_info(g, target_ret_ty);
  if !dst.ok || src.is_result != dst.is_result {
    return GenExprResult { ok: false, err: "function return type incompatible with `?`", g: g, val: ir.v_int(0), ty: g.ctx.ty_bad };
  }

  let rs: GenNewIdResult = gen_new_slot(g);
  g = rs.g;
  let out_slot: i32 = rs.id;
  g = gen_emit(g, ir.Instr.SlotDecl(out_slot, out_ty));

  let base: i32 = g.blocks.len();
  let bok: GenNewIdResult = gen_new_block(g, "try_ok_".concat(base.to_string()));
  g = bok.g;
  let ok_idx_block: i32 = bok.id;
  let bres: GenNewIdResult = gen_new_block(g, "try_res_".concat(base.to_string()));
  g = bres.g;
  let res_idx_block: i32 = bres.id;
  let bend: GenNewIdResult = gen_new_block(g, "try_end_".concat(base.to_string()));
  g = bend.g;
  let end_idx_block: i32 = bend.id;

  let tr0: GenNewIdResult = gen_new_temp(g);
  g = tr0.g;
  let tag_tid: i32 = tr0.id;
  g = gen_emit(g, ir.Instr.EnumTag(tag_tid, ar.val));

  let tr1: GenNewIdResult = gen_new_temp(g);
  g = tr1.g;
  let is_ok_tid: i32 = tr1.id;
  g = gen_emit(g, ir.Instr.Cmp(is_ok_tid, ir.CmpKind.Eq, g.ctx.ty_i32, ir.v_temp(tag_tid), ir.v_int(src.ok_idx)));
  g = gen_term(g, ir.Term.CondBr(ir.v_temp(is_ok_tid), g.blocks.get(ok_idx_block).name, g.blocks.get(res_idx_block).name));

  g = gen_set_block(g, ok_idx_block);
  let tr2: GenNewIdResult = gen_new_temp(g);
  g = tr2.g;
  let ok_tid: i32 = tr2.id;
  g = gen_emit(g, ir.Instr.EnumPayload(ok_tid, out_ty, ar.val, src.ok_name, 0));
  g = gen_emit(g, ir.Instr.Store(out_slot, ir.v_temp(ok_tid)));
  if !g.blocks.get(g.cur).has_term {
    g = gen_term(g, ir.Term.Br(g.blocks.get(end_idx_block).name));
  }

  g = gen_set_block(g, res_idx_block);
  let mut rv: ir.Value = ir.v_int(0);
  if src.is_result {
    let tr3: GenNewIdResult = gen_new_temp(g);
    g = tr3.g;
    let err_tid: i32 = tr3.id;
    g = gen_emit(g, ir.Instr.EnumPayload(err_tid, src.residual_ty, ar.val, src.residual_name, 0));
    let mut err_v: ir.Value = ir.v_temp(err_tid);
    let cv: TryErrConvResult = try_resolve_err_conv(g, src.residual_ty, dst.residual_ty);
    if !cv.ok { return GenExprResult { ok: false, err: cv.err, g: g, val: ir.v_int(0), ty: g.ctx.ty_bad }; }
    g = cv.g;
    if cv.need_call {
      let trc: GenNewIdResult = gen_new_temp(g);
      g = trc.g;
      let cv_tid: i32 = trc.id;
      let mut cv_args: Vec[ir.Value] = Vec();
      cv_args.push(err_v);
      g = gen_emit(g, ir.Instr.Call(true, cv_tid, cv.ret_ty, qname(cv.mod_path, cv.fn_name), cv_args));
      err_v = ir.v_temp(cv_tid);
    }
    let tr4: GenNewIdResult = gen_new_temp(g);
    g = tr4.g;
    let ret_tid: i32 = tr4.id;
    let mut ps: Vec[ir.Value] = Vec();
    ps.push(err_v);
    g = gen_emit(g, ir.Instr.EnumInit(ret_tid, target_ret_ty, dst.residual_name, ps));
    rv = ir.v_temp(ret_tid);
  } else {
    let tr5: GenNewIdResult = gen_new_temp(g);
    g = tr5.g;
    let ret_tid2: i32 = tr5.id;
    g = gen_emit(g, ir.Instr.EnumInit(ret_tid2, target_ret_ty, dst.residual_name, Vec()));
    rv = ir.v_temp(ret_tid2);
  }
  if ct.found {
    g = gen_emit(g, ir.Instr.Store(ct.t.ret_slot, rv));
    g = gen_term(g, ir.Term.Br(ct.t.end_blk));
  } else {
    g = gen_term(g, ir.Term.Ret(true, rv));
  }

  g = gen_set_block(g, end_idx_block);
  let tr6: GenNewIdResult = gen_new_temp(g);
  g = tr6.g;
  let out_tid: i32 = tr6.id;
  g = gen_emit(g, ir.Instr.Load(out_tid, out_ty, out_slot));
  return GenExprResult { ok: true, err: "", g: g, val: ir.v_temp(out_tid), ty: out_ty };
}

fn gen_expr_try_block(g0: Gen, bid: i32, out_ty: i32) -> GenExprResult {
  let mut g: Gen = g0;
  let dst: TryEnumInfo = try_enum_info(g, out_ty);
  if !dst.ok { return GenExprResult { ok: false, err: "`try {}` requires Result/Option output type", g: g, val: ir.v_int(0), ty: g.ctx.ty_bad }; }

  let rs: GenNewIdResult = gen_new_slot(g);
  g = rs.g;
  let out_slot: i32 = rs.id;
  g = gen_emit(g, ir.Instr.SlotDecl(out_slot, out_ty));

  let base: i32 = g.blocks.len();
  let bend: GenNewIdResult = gen_new_block(g, "tryblk_end_".concat(base.to_string()));
  g = bend.g;
  let end_idx: i32 = bend.id;
  let end_name: String = g.blocks.get(end_idx).name;

  let mut tries: Vec[TryCtx] = g.tries;
  tries.push(TryCtx { ret_slot: out_slot, end_blk: end_name, ret_ty: out_ty });
  g.tries = tries;
  let br: GenExprResult = gen_expr(g, bid, -1);
  if !br.ok { return br; }
  g = br.g;
  g.tries = try_stack_pop(g.tries);

  if !g.blocks.get(g.cur).has_term {
    if br.ty == out_ty {
      g = gen_emit(g, ir.Instr.Store(out_slot, br.val));
    } else {
      let rt: GenNewIdResult = gen_new_temp(g);
      g = rt.g;
      let tid: i32 = rt.id;
      let mut ps: Vec[ir.Value] = Vec();
      ps.push(br.val);
      g = gen_emit(g, ir.Instr.EnumInit(tid, out_ty, dst.ok_name, ps));
      g = gen_emit(g, ir.Instr.Store(out_slot, ir.v_temp(tid)));
    }
    g = gen_term(g, ir.Term.Br(end_name));
  }

  g = gen_set_block(g, end_idx);
  let rt2: GenNewIdResult = gen_new_temp(g);
  g = rt2.g;
  let out_tid: i32 = rt2.id;
  g = gen_emit(g, ir.Instr.Load(out_tid, out_ty, out_slot));
  return GenExprResult { ok: true, err: "", g: g, val: ir.v_temp(out_tid), ty: out_ty };
}

fn gen_expr_as(g0: Gen, a: i32, _tn: ast.TypeName, out_ty: i32) -> GenExprResult {
  let mut g: Gen = g0;
  let ar: GenExprResult = gen_expr(g, a, -1);
  if !ar.ok { return ar; }
  g = ar.g;

  let from_base: i32 = tc.strip_range(g.ctx, ar.ty);
  let to_base: i32 = tc.strip_range(g.ctx, out_ty);

  // 1) Base cast (i32<->i64). Range types are represented as their base in IR.
  let mut v: ir.Value = ar.val;
  if from_base != to_base {
    let rt: GenNewIdResult = gen_new_temp(g);
    g = rt.g;
    let tid: i32 = rt.id;

    let from_float: bool = tc.is_float_ty(g.ctx, from_base);
    let to_float: bool = tc.is_float_ty(g.ctx, to_base);
    if to_float {
      // int->float and float->float
      g = gen_emit(g, ir.Instr.FloatCast(tid, to_base, from_base, ar.val));
    } else if from_float {
      // float->int (checked at runtime)
      g = gen_emit(g, ir.Instr.IntCastChecked(tid, to_base, from_base, ar.val));
    } else {
      // int->int
      g = gen_emit(g, ir.Instr.IntCastChecked(tid, to_base, from_base, ar.val));
    }
    v = ir.v_temp(tid);
  }

  // 2) Refined cast: add a runtime range check for `@range(..)` targets.
  if tc.is_range_ty(g.ctx, out_ty) {
    let rt2: ir.Ty = ir.ty_pool_get(g.ctx.pool, out_ty);
    g = gen_emit(g, ir.Instr.RangeCheck(to_base, v, rt2.lo, rt2.hi));
  }

  return GenExprResult { ok: true, err: "", g: g, val: v, ty: out_ty };
}

fn gen_expr_block(g0: Gen, b: ast.ExprBlock, out_ty: i32) -> GenExprResult {
  let mut g: Gen = g0;
  g.slots = slots_enter(g.slots);
  g.locals = tc.locals_enter(g.locals);
  let mut diverges: bool = false;

  let mut i: i32 = 0;
  while i < b.stmts.len() {
    let r: GenStmtResult = gen_stmt(g, b.stmts.get(i));
    if !r.ok { return GenExprResult { ok: false, err: r.err, g: r.g, val: ir.v_int(0), ty: g0.ctx.ty_bad }; }
    g = r.g;
    let blk: ir.Block = g.blocks.get(g.cur);
    if blk.has_term {
      diverges = true;
      i = b.stmts.len();
    } else {
      i = i + 1;
    }
  }

  let mut val: ir.Value = ir.v_int(0);
  if !diverges && b.has_tail {
    let er: GenExprResult = gen_expr(g, b.tail, out_ty);
    if !er.ok { return er; }
    g = er.g;
    val = er.val;
  }

  g.locals = tc.locals_exit(g.locals);
  g.slots = slots_exit(g.slots);
  return GenExprResult { ok: true, err: "", g: g, val: val, ty: out_ty };
}

fn gen_expr_int(g0: Gen, text: String, ty: i32, expected: i32) -> GenExprResult {
  let mut g: Gen = g0;
  let rt: GenNewIdResult = gen_new_temp(g);
  g = rt.g;
  let tid: i32 = rt.id;
  let use_ty: i32 = if expected != -1 { expected } else { ty };
  let base: i32 = tc.strip_range(g.ctx, use_ty);
  let bt: ir.Ty = ir.ty_pool_get(g.ctx.pool, base);
  let is_u64_like: bool = match bt.kind { ir.TyKind.U64 => true, ir.TyKind.USize => true, _ => false };
  let mut lit_bits: i64 = 0;
  if is_u64_like {
    let pru: tc.ParseU64DecResult = tc.parse_u64_dec(text);
    if !pru.ok { return GenExprResult { ok: false, err: "integer literal out of range", g: g, val: ir.v_int(0), ty: g.ctx.ty_bad }; }
    lit_bits = pru.val;
  } else {
    let pr: tc.ParseI64DecResult = tc.parse_i64_dec(text);
    if !pr.ok { return GenExprResult { ok: false, err: "integer literal out of range", g: g, val: ir.v_int(0), ty: g.ctx.ty_bad }; }
    lit_bits = pr.val;
  }
  let vv: ir.Value = ir.v_i64(lit_bits);
  g = gen_emit(g, ir.Instr.Const(tid, use_ty, vv));
  return GenExprResult { ok: true, err: "", g: g, val: ir.v_temp(tid), ty: use_ty };
}

fn gen_expr_float(g0: Gen, text: String, ty: i32, expected: i32) -> GenExprResult {
  let mut g: Gen = g0;
  let rt: GenNewIdResult = gen_new_temp(g);
  g = rt.g;
  let tid: i32 = rt.id;
  let use_ty: i32 = if expected != -1 { expected } else { ty };
  let mut lit: String = tc.float_strip_suffix_text(text);
  if use_ty == g.ctx.ty_f32 { lit = lit.concat("f"); }
  g = gen_emit(g, ir.Instr.Const(tid, use_ty, ir.v_float(lit)));
  return GenExprResult { ok: true, err: "", g: g, val: ir.v_temp(tid), ty: use_ty };
}

fn gen_expr_bool(g0: Gen, b: bool, ty: i32) -> GenExprResult {
  let mut g: Gen = g0;
  let rt: GenNewIdResult = gen_new_temp(g);
  g = rt.g;
  let tid: i32 = rt.id;
  g = gen_emit(g, ir.Instr.Const(tid, ty, ir.v_bool(b)));
  return GenExprResult { ok: true, err: "", g: g, val: ir.v_temp(tid), ty: ty };
}

fn gen_expr_str(g0: Gen, s: String, ty: i32) -> GenExprResult {
  let mut g: Gen = g0;
  let rt: GenNewIdResult = gen_new_temp(g);
  g = rt.g;
  let tid: i32 = rt.id;
  g = gen_emit(g, ir.Instr.Const(tid, ty, ir.v_str(s)));
  return GenExprResult { ok: true, err: "", g: g, val: ir.v_temp(tid), ty: ty };
}

fn gen_expr_ident(g0: Gen, name: String, ty: i32) -> GenExprResult {
  let sr: LookupSlotResult = slots_lookup(g0.slots, name);
  if !sr.found {
    let lcr: tc.LookupConstResult = tc.locals_lookup_const(g0.locals, name);
    if lcr.found {
      if !lcr.has_val { return GenExprResult { ok: false, err: "unbound const param", g: g0, val: ir.v_int(0), ty: g0.ctx.ty_bad }; }
      let mut g1: Gen = g0;
      let rt0: GenNewIdResult = gen_new_temp(g1);
      g1 = rt0.g;
      let tid0: i32 = rt0.id;
      g1 = gen_emit(g1, ir.Instr.Const(tid0, lcr.ty, ir.v_i64(lcr.iv)));
      return GenExprResult { ok: true, err: "", g: g1, val: ir.v_temp(tid0), ty: lcr.ty };
    }
    // Const reference (inlined; no globals in v0).
    let ni: tc.NamedConstImport = tc.find_named_const(g0.imps.named_consts, name);
    let want_mod: String = if ni.local != "" { ni.mod_path } else { g0.mod_path };
    let want_name: String = if ni.local != "" { ni.name } else { name };
    let cs: tc.ConstSig = tc.find_const(g0.ctx, want_mod, want_name);
    if cs.name == "" { return GenExprResult { ok: false, err: "unknown var/const", g: g0, val: ir.v_int(0), ty: g0.ctx.ty_bad }; }
    let inl: GenInlineConstResult = gen_inline_const_sig(g0, cs);
    if !inl.ok { return GenExprResult { ok: false, err: inl.err, g: inl.g, val: ir.v_int(0), ty: g0.ctx.ty_bad }; }
    return GenExprResult { ok: true, err: "", g: inl.g, val: inl.val, ty: inl.ty };
  }
  let mut g: Gen = g0;
  let rt: GenNewIdResult = gen_new_temp(g);
  g = rt.g;
  let tid: i32 = rt.id;
  g = gen_emit(g, ir.Instr.Load(tid, ty, sr.slot));
  return GenExprResult { ok: true, err: "", g: g, val: ir.v_temp(tid), ty: ty };
}

fn gen_expr_unary(g0: Gen, op: ast.UnaryOp, a: i32, out_ty: i32) -> GenExprResult {
  let mut g: Gen = g0;
  let ar: GenExprResult = gen_expr(g, a, -1);
  if !ar.ok { return ar; }
  g = ar.g;
  let is_not: bool = match op { ast.UnaryOp.Not => true, _ => false };
  let is_pos: bool = match op { ast.UnaryOp.Pos => true, _ => false };
  if is_pos {
    return GenExprResult { ok: true, err: "", g: g, val: ar.val, ty: out_ty };
  }
  if is_not {
    if out_ty == g.ctx.ty_bool {
      let rt: GenNewIdResult = gen_new_temp(g);
      g = rt.g;
      let tid: i32 = rt.id;
      g = gen_emit(g, ir.Instr.Not(tid, ar.val));
      return GenExprResult { ok: true, err: "", g: g, val: ir.v_temp(tid), ty: out_ty };
    }
    if tc.is_int_like_ty(g.ctx, out_ty) {
      let mt: GenNewIdResult = gen_new_temp(g);
      g = mt.g;
      let mid: i32 = mt.id;
      g = gen_emit(g, ir.Instr.Const(mid, out_ty, ir.v_i64(-1)));

      let rt2: GenNewIdResult = gen_new_temp(g);
      g = rt2.g;
      let tid2: i32 = rt2.id;
      g = gen_emit(g, ir.Instr.BinOp(tid2, ir.BinOpKind.BitXor, out_ty, ar.val, ir.v_temp(mid)));
      return GenExprResult { ok: true, err: "", g: g, val: ir.v_temp(tid2), ty: out_ty };
    }
    return GenExprResult { ok: false, err: "unsupported unary ! type", g: g, val: ir.v_int(0), ty: g.ctx.ty_bad };
  }
  // Neg: 0 - x
  let zt: GenNewIdResult = gen_new_temp(g);
  g = zt.g;
  let z: i32 = zt.id;
  let z0: ir.Value = if tc.is_float_ty(g.ctx, out_ty) { ir.v_float("0.0") } else { ir.v_i64(0) };
  g = gen_emit(g, ir.Instr.Const(z, out_ty, z0));
  let rt2: GenNewIdResult = gen_new_temp(g);
  g = rt2.g;
  let tid2: i32 = rt2.id;
  g = gen_emit(g, ir.Instr.BinOp(tid2, ir.BinOpKind.Sub, out_ty, ir.v_temp(z), ar.val));
  return GenExprResult { ok: true, err: "", g: g, val: ir.v_temp(tid2), ty: out_ty };
}

fn gen_expr_binary(g0: Gen, op: ast.BinaryOp, l: i32, r: i32, out_ty: i32) -> GenExprResult {
  let mut g: Gen = g0;
  let lr: GenExprResult = gen_expr(g, l, -1);
  if !lr.ok { return lr; }
  g = lr.g;

  // Short-circuit logical ops: do not evaluate RHS eagerly.
  let is_logic: bool = match op { ast.BinaryOp.AndAnd => true, ast.BinaryOp.OrOr => true, _ => false };
  if is_logic {
    return gen_expr_short_circuit_logic(g, op, lr.val, r, out_ty);
  }

  let rr: GenExprResult = gen_expr(g, r, lr.ty);
  if !rr.ok { return rr; }
  g = rr.g;

  // Special-case: enum equality against unit variants lowers to tag comparison.
  let is_eqop: bool = match op { ast.BinaryOp.Eq => true, ast.BinaryOp.Ne => true, _ => false };
  if is_eqop && tc.is_enum_ty(g.ctx, lr.ty) {
    let lt: i32 = enum_unit_tag(g, l, lr.ty);
    let rt: i32 = enum_unit_tag(g, r, lr.ty);
    if lt != -1 || rt != -1 {
      let unit_tag: i32 = if lt != -1 { lt } else { rt };
      let other: ir.Value = if lt != -1 { rr.val } else { lr.val };

      let nt: GenNewIdResult = gen_new_temp(g);
      g = nt.g;
      let tag_id: i32 = nt.id;
      g = gen_emit(g, ir.Instr.EnumTag(tag_id, other));

      let cmp: GenNewIdResult = gen_new_temp(g);
      g = cmp.g;
      let cmp_id: i32 = cmp.id;
      let ck: ir.CmpKind = if match op { ast.BinaryOp.Eq => true, _ => false } { ir.CmpKind.Eq } else { ir.CmpKind.Ne };
      g = gen_emit(g, ir.Instr.Cmp(cmp_id, ck, g.ctx.ty_i32, ir.v_temp(tag_id), ir.v_int(unit_tag)));
      return GenExprResult { ok: true, err: "", g: g, val: ir.v_temp(cmp_id), ty: out_ty };
    }
  }

  let is_cmp: bool = match op { ast.BinaryOp.Lt => true, ast.BinaryOp.Le => true, ast.BinaryOp.Gt => true, ast.BinaryOp.Ge => true, ast.BinaryOp.Eq => true, ast.BinaryOp.Ne => true, _ => false };
  if is_cmp {
    let rt: GenNewIdResult = gen_new_temp(g);
    g = rt.g;
    let tid: i32 = rt.id;
    let ck: ir.CmpKind = match op {
      ast.BinaryOp.Lt => ir.CmpKind.Lt,
      ast.BinaryOp.Le => ir.CmpKind.Le,
      ast.BinaryOp.Gt => ir.CmpKind.Gt,
      ast.BinaryOp.Ge => ir.CmpKind.Ge,
      ast.BinaryOp.Eq => ir.CmpKind.Eq,
      _ => ir.CmpKind.Ne,
    };
    g = gen_emit(g, ir.Instr.Cmp(tid, ck, lr.ty, lr.val, rr.val));
    return GenExprResult { ok: true, err: "", g: g, val: ir.v_temp(tid), ty: out_ty };
  }

  let rt2: GenNewIdResult = gen_new_temp(g);
  g = rt2.g;
  let tid2: i32 = rt2.id;
  let bk: ir.BinOpKind = match op {
    ast.BinaryOp.Add => ir.BinOpKind.Add,
    ast.BinaryOp.Sub => ir.BinOpKind.Sub,
    ast.BinaryOp.Mul => ir.BinOpKind.Mul,
    ast.BinaryOp.Div => ir.BinOpKind.Div,
    ast.BinaryOp.Mod => ir.BinOpKind.Mod,
    ast.BinaryOp.BitAnd => ir.BinOpKind.BitAnd,
    ast.BinaryOp.BitOr => ir.BinOpKind.BitOr,
    ast.BinaryOp.BitXor => ir.BinOpKind.BitXor,
    ast.BinaryOp.Shl => ir.BinOpKind.Shl,
    _ => ir.BinOpKind.Shr,
  };
  g = gen_emit(g, ir.Instr.BinOp(tid2, bk, out_ty, lr.val, rr.val));
  return GenExprResult { ok: true, err: "", g: g, val: ir.v_temp(tid2), ty: out_ty };
}

fn gen_expr_short_circuit_logic(g0: Gen, op: ast.BinaryOp, lhs: ir.Value, rhs_id: i32, out_ty: i32) -> GenExprResult {
  let mut g: Gen = g0;
  if out_ty != g.ctx.ty_bool { return GenExprResult { ok: false, err: "logical op must be bool", g: g, val: ir.v_int(0), ty: g.ctx.ty_bad }; }

  // Use a slot for the result (matches if-expr lowering pattern).
  let rs: GenNewIdResult = gen_new_slot(g);
  g = rs.g;
  let res_slot: i32 = rs.id;
  g = gen_emit(g, ir.Instr.SlotDecl(res_slot, out_ty));
  g = gen_emit(g, ir.Instr.Store(res_slot, ir.v_bool(false)));

  let base: i32 = g.blocks.len();
  let r_rhs: GenNewIdResult = gen_new_block(g, "logic_rhs_".concat(base.to_string()));
  g = r_rhs.g;
  let rhs_idx: i32 = r_rhs.id;
  let r_sc: GenNewIdResult = gen_new_block(g, "logic_sc_".concat(base.to_string()));
  g = r_sc.g;
  let sc_idx: i32 = r_sc.id;
  let r_end: GenNewIdResult = gen_new_block(g, "logic_end_".concat(base.to_string()));
  g = r_end.g;
  let end_idx: i32 = r_end.id;

  let is_and: bool = match op { ast.BinaryOp.AndAnd => true, _ => false };
  if is_and {
    g = gen_term(g, ir.Term.CondBr(lhs, g.blocks.get(rhs_idx).name, g.blocks.get(sc_idx).name));
  } else {
    g = gen_term(g, ir.Term.CondBr(lhs, g.blocks.get(sc_idx).name, g.blocks.get(rhs_idx).name));
  }

  // Short-circuit block.
  g = gen_set_block(g, sc_idx);
  if is_and {
    g = gen_emit(g, ir.Instr.Store(res_slot, ir.v_bool(false)));
  } else {
    g = gen_emit(g, ir.Instr.Store(res_slot, ir.v_bool(true)));
  }
  g = gen_term(g, ir.Term.Br(g.blocks.get(end_idx).name));

  // RHS block.
  g = gen_set_block(g, rhs_idx);
  let rr: GenExprResult = gen_expr(g, rhs_id, g.ctx.ty_bool);
  if !rr.ok { return rr; }
  g = rr.g;
  g = gen_emit(g, ir.Instr.Store(res_slot, rr.val));
  if !g.blocks.get(g.cur).has_term {
    g = gen_term(g, ir.Term.Br(g.blocks.get(end_idx).name));
  }

  // End block.
  g = gen_set_block(g, end_idx);
  let rt: GenNewIdResult = gen_new_temp(g);
  g = rt.g;
  let tid: i32 = rt.id;
  g = gen_emit(g, ir.Instr.Load(tid, out_ty, res_slot));
  return GenExprResult { ok: true, err: "", g: g, val: ir.v_temp(tid), ty: out_ty };
}

fn enum_unit_tag(g0: Gen, id: i32, enum_ty: i32) -> i32 {
  if !tc.is_enum_ty(g0.ctx, enum_ty) { return -1; }
  let n: ast.ExprNode = ast.expr_pool_get(g0.exprs, id);

  if match n { ast.ExprNode.Call(_c, _ta, _args) => true, _ => false } {
    let callee: i32 = match n { ast.ExprNode.Call(c, _ta, _args) => c, _ => -1 };
    let targs: Vec[ast.TypeName] = match n { ast.ExprNode.Call(_c, ta, _args) => ta, _ => Vec() };
    let args: Vec[i32] = match n { ast.ExprNode.Call(_c, _ta, a) => a, _ => Vec() };
    if targs.len() == 0 && args.len() == 0 { return enum_unit_tag(g0, callee, enum_ty); }
    return -1;
  }

  let var_name: String = match n {
    ast.ExprNode.DotIdent(s) => s,
    ast.ExprNode.Member(_recv, s) => s,
    _ => "",
  };
  if var_name == "" { return -1; }

  let et: ir.Ty = ir.ty_pool_get(g0.ctx.pool, enum_ty);
  if et.kind != ir.TyKind.Enum { return -1; }
  let fe: tc.FindEnumResult = tc.find_enum(g0.ctx, tc.ty_mod(et), tc.ty_name(et));
  if !fe.found { return -1; }
  let es: tc.EnumSig = fe.sig;
  let mut vi: i32 = 0;
  while vi < es.vars.len() {
    let v: tc.EnumVariantSig = es.vars.get(vi);
    if v.name == var_name {
      if v.fields.len() != 0 { return -1; }
      return vi;
    }
    vi = vi + 1;
  }
  return -1;
}

// Implementations live in gen_cf.vox / gen_member_struct.vox / gen_call_match.vox.
