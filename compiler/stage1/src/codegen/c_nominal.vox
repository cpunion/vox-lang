import "ir" as ir

struct NomInfo { name: String, kind: i32 } // 0=struct, 1=enum

fn emitted_has(emitted: Vec[String], name: String) -> bool {
  let mut i: i32 = 0;
  while i < emitted.len() {
    if emitted.get(i) == name { return true; }
    i = i + 1;
  }
  return false;
}

fn emit_struct_typedef(pool: ir.TyPool, st: ir.StructDecl) -> Vec[String] {
  let mut out: Vec[String] = Vec();
  out.push("typedef struct {");
  let mut i: i32 = 0;
  while i < st.fields.len() {
    let f: ir.StructField = st.fields.get(i);
    out.push("  ".concat(c_type(pool, f.ty)).concat(" ").concat(c_ident(f.name)).concat(";"));
    i = i + 1;
  }
  out.push("} ".concat(c_struct_type_name(st.name)).concat(";"));
  return out;
}

fn enum_has_payload(en: ir.EnumDecl) -> bool {
  let mut vi: i32 = 0;
  while vi < en.variants.len() {
    if en.variants.get(vi).fields.len() != 0 { return true; }
    vi = vi + 1;
  }
  return false;
}

fn emit_enum_typedef(pool: ir.TyPool, en: ir.EnumDecl) -> Vec[String] {
  let mut out: Vec[String] = Vec();
  let has_payload: bool = enum_has_payload(en);
  out.push("typedef struct {");
  out.push("  int32_t tag;");
  if has_payload {
    out.push("  union {");
    let mut vi: i32 = 0;
    while vi < en.variants.len() {
      let v: ir.EnumVariant = en.variants.get(vi);
      out.push("    struct {");
      if v.fields.len() != 0 {
        let mut fi: i32 = 0;
        while fi < v.fields.len() {
          out.push("      ".concat(c_type(pool, v.fields.get(fi))).concat(" _").concat(fi.to_string()).concat(";"));
          fi = fi + 1;
        }
      } else {
        out.push("      uint8_t _;");
      }
      out.push("    } ".concat(c_ident(v.name)).concat(";"));
      vi = vi + 1;
    }
    out.push("  } payload;");
  }
  out.push("} ".concat(c_enum_type_name(en.name)).concat(";"));
  return out;
}

fn deps_satisfied(pool: ir.TyPool, p: ir.Program, info: NomInfo, emitted: Vec[String]) -> bool {
  if info.kind == 0 {
    let st: ir.StructDecl = find_struct(p, info.name);
    let mut i: i32 = 0;
    while i < st.fields.len() {
      let f: ir.StructField = st.fields.get(i);
      let t: ir.Ty = ir.ty_pool_get(pool, f.ty);
      if t.kind == ir.TyKind.Struct || t.kind == ir.TyKind.Enum {
        if !emitted_has(emitted, ty_qname(t)) { return false; }
      }
      i = i + 1;
    }
    return true;
  }

  let en: ir.EnumDecl = find_enum(p, info.name);
  let mut vi: i32 = 0;
  while vi < en.variants.len() {
    let v: ir.EnumVariant = en.variants.get(vi);
    let mut fi: i32 = 0;
    while fi < v.fields.len() {
      let t: ir.Ty = ir.ty_pool_get(pool, v.fields.get(fi));
      if t.kind == ir.TyKind.Struct || t.kind == ir.TyKind.Enum {
        if !emitted_has(emitted, ty_qname(t)) { return false; }
      }
      fi = fi + 1;
    }
    vi = vi + 1;
  }
  return true;
}

fn find_struct(p: ir.Program, name: String) -> ir.StructDecl {
  let mut i: i32 = 0;
  while i < p.structs.len() {
    let st: ir.StructDecl = p.structs.get(i);
    if st.name == name { return st; }
    i = i + 1;
  }
  panic("unknown struct in codegen");
  return ir.StructDecl { name: "", fields: Vec() };
}

fn find_enum(p: ir.Program, name: String) -> ir.EnumDecl {
  let mut i: i32 = 0;
  while i < p.enums.len() {
    let en: ir.EnumDecl = p.enums.get(i);
    if en.name == name { return en; }
    i = i + 1;
  }
  panic("unknown enum in codegen");
  return ir.EnumDecl { name: "", variants: Vec() };
}

pub fn emit_nominal_types(pool: ir.TyPool, p: ir.Program) -> Vec[String] {
  let mut infos: Vec[NomInfo] = Vec();
  let mut si: i32 = 0;
  while si < p.structs.len() { infos.push(NomInfo { name: p.structs.get(si).name, kind: 0 }); si = si + 1; }
  let mut ei: i32 = 0;
  while ei < p.enums.len() { infos.push(NomInfo { name: p.enums.get(ei).name, kind: 1 }); ei = ei + 1; }

  let mut out: Vec[String] = Vec();
  let mut emitted: Vec[String] = Vec();
  while emitted.len() < infos.len() {
    let mut progress: bool = false;
    let mut i: i32 = 0;
    while i < infos.len() {
      let info: NomInfo = infos.get(i);
      if emitted_has(emitted, info.name) { i = i + 1; continue; }
      if !deps_satisfied(pool, p, info, emitted) { i = i + 1; continue; }

      let lines: Vec[String] = if info.kind == 0 { emit_struct_typedef(pool, find_struct(p, info.name)) } else { emit_enum_typedef(pool, find_enum(p, info.name)) };
      let mut li: i32 = 0;
      while li < lines.len() { out.push(lines.get(li)); li = li + 1; }
      out.push("");
      emitted.push(info.name);
      progress = true;
      i = i + 1;
    }
    if !progress {
      panic("cyclic nominal types by-value dependency");
    }
  }
  return out;
}

