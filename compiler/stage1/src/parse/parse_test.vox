import "std/testing" as t
import "parse" as p
import "ast" as ast

fn ok(e: p.ParseError) -> bool {
  // Stage0 supports `==` for enums only when comparing against a unit variant.
  // `ParseError.None` is a unit variant, so this is the simplest check.
  return e == p.ParseError.None;
}

fn ty_is0(tn: ast.TypeName, name: String) -> bool {
  return tn.parts.len() == 1 && tn.parts.get(0) == name && tn.args.len() == 0;
}

fn ty_is_vec1(tn: ast.TypeName, elem: String) -> bool {
  return tn.parts.len() == 1 && tn.parts.get(0) == "Vec" && tn.args.len() == 1 && ty_is0(tn.args.get(0), elem);
}

fn test_parse_single_fn_return_int() -> () {
  let r: p.ParseResult = p.parse_text("fn main() -> i32 { return 0; }");
  t.assert(ok(r.err));

  let prog: ast.Program = r.prog;
  let imports: Vec[ast.ImportDecl] = prog.imports;
  t.assert_eq(imports.len(), 0);
  t.assert_eq(prog.structs.len(), 0);
  t.assert_eq(prog.enums.len(), 0);
  let funcs: Vec[ast.FuncDecl] = prog.funcs;
  t.assert_eq(funcs.len(), 1);
  let exprs: ast.ExprPool = prog.exprs;

  let f: ast.FuncDecl = funcs.get(0);
  t.assert_eq(f.name, "main");
  t.assert_eq(f.type_params.len(), 0);
  let params: Vec[ast.Param] = f.params;
  t.assert_eq(params.len(), 0);
  t.assert(ty_is0(f.ret, "i32"));
  let stmts: Vec[ast.Stmt] = f.body.stmts;
  t.assert_eq(stmts.len(), 1);

  let st: ast.Stmt = stmts.get(0);
  let is_ret0: bool = match st {
    ast.Stmt.Return(has, id) => has && match ast.expr_pool_get(exprs, id) {
      ast.ExprNode.Int(v) => v == 0,
      _ => false,
    },
    _ => false,
  };
  t.assert(is_ret0);
}

fn test_parse_import_as_alias() -> () {
  let r: p.ParseResult = p.parse_text("import \"dep\" as d\nfn main() -> i32 { return 0; }");
  t.assert(ok(r.err));

  let prog: ast.Program = r.prog;
  let imports: Vec[ast.ImportDecl] = prog.imports;
  t.assert_eq(imports.len(), 1);
  let imp: ast.ImportDecl = imports.get(0);
  t.assert_eq(imp.path, "dep");
  t.assert_eq(imp.alias, "d");
  t.assert_eq(imp.names.len(), 0);

  let funcs: Vec[ast.FuncDecl] = prog.funcs;
  t.assert_eq(funcs.len(), 1);
  t.assert_eq(funcs.get(0).name, "main");
}

fn test_parse_named_import_from() -> () {
  let src: String = "import { one as uno, two } from \"dep\"\nfn main() -> i32 { return 0; }";
  let r: p.ParseResult = p.parse_text(src);
  t.assert(ok(r.err));
  t.assert_eq(r.prog.imports.len(), 1);
  let imp: ast.ImportDecl = r.prog.imports.get(0);
  t.assert_eq(imp.path, "dep");
  t.assert_eq(imp.alias, "");
  t.assert_eq(imp.names.len(), 2);
  t.assert_eq(imp.names.get(0).name, "one");
  t.assert_eq(imp.names.get(0).alias, "uno");
  t.assert_eq(imp.names.get(1).name, "two");
  t.assert_eq(imp.names.get(1).alias, "");
}

fn test_parse_generic_type_names_with_brackets() -> () {
  let src: String = "fn f(xs: Vec[i32]) -> Vec[String] { return xs; }";
  let r: p.ParseResult = p.parse_text(src);
  t.assert(ok(r.err));

  let f: ast.FuncDecl = r.prog.funcs.get(0);
  t.assert_eq(f.type_params.len(), 0);
  t.assert_eq(f.params.len(), 1);
  let p0: ast.Param = f.params.get(0);
  t.assert_eq(p0.name, "xs");
  t.assert(ty_is_vec1(p0.ty, "i32"));
  t.assert(ty_is_vec1(f.ret, "String"));
}

fn test_parse_string_escapes_newline() -> () {
  let src: String = "fn main() -> i32 { print(\"a\\n\"); return 0; }";
  let r: p.ParseResult = p.parse_text(src);
  t.assert(ok(r.err));

  let f: ast.FuncDecl = r.prog.funcs.get(0);
  t.assert_eq(f.body.stmts.len(), 2);
  let st0: ast.Stmt = f.body.stmts.get(0);
  let exprs: ast.ExprPool = r.prog.exprs;

  let ok0: bool = match st0 {
    ast.Stmt.Expr(eid) => match ast.expr_pool_get(exprs, eid) {
      ast.ExprNode.Call(_callee, _targs, args) =>
        args.len() == 1 && match ast.expr_pool_get(exprs, args.get(0)) {
          ast.ExprNode.Str(s) => s == "a\n",
          _ => false,
        },
      _ => false,
    },
    _ => false,
  };
  t.assert(ok0);
}

fn test_parse_fn_type_params() -> () {
  let src: String = "fn id[T](x: T) -> T { return x; }";
  let r: p.ParseResult = p.parse_text(src);
  t.assert(ok(r.err));
  let f: ast.FuncDecl = r.prog.funcs.get(0);
  t.assert_eq(f.type_params.len(), 1);
  t.assert_eq(f.type_params.get(0), "T");
  t.assert_eq(f.params.len(), 1);
  t.assert(ty_is0(f.params.get(0).ty, "T"));
  t.assert(ty_is0(f.ret, "T"));
}

fn test_parse_struct_decl_and_pub_fields() -> () {
  let src: String = "pub struct S { pub x: i32, y: i32 }\nfn main() -> i32 { return 0; }";
  let r: p.ParseResult = p.parse_text(src);
  t.assert(ok(r.err));

  t.assert_eq(r.prog.structs.len(), 1);
  let s: ast.StructDecl = r.prog.structs.get(0);
  t.assert(s.is_pub);
  t.assert_eq(s.name, "S");
  t.assert_eq(s.fields.len(), 2);
  let f0: ast.FieldDecl = s.fields.get(0);
  t.assert(f0.is_pub);
  t.assert_eq(f0.name, "x");
  t.assert(ty_is0(f0.ty, "i32"));
  let f1: ast.FieldDecl = s.fields.get(1);
  t.assert(!f1.is_pub);
  t.assert_eq(f1.name, "y");
  t.assert(ty_is0(f1.ty, "i32"));
}

fn test_parse_enum_decl_variants() -> () {
  let src: String = "enum E { A(i32), B(String), None }\nfn main() -> i32 { return 0; }";
  let r: p.ParseResult = p.parse_text(src);
  t.assert(ok(r.err));

  t.assert_eq(r.prog.enums.len(), 1);
  let e: ast.EnumDecl = r.prog.enums.get(0);
  t.assert(!e.is_pub);
  t.assert_eq(e.name, "E");
  t.assert_eq(e.variants.len(), 3);

  let v0: ast.EnumVariantDecl = e.variants.get(0);
  t.assert_eq(v0.name, "A");
  t.assert_eq(v0.fields.len(), 1);
  t.assert(ty_is0(v0.fields.get(0), "i32"));

  let v1: ast.EnumVariantDecl = e.variants.get(1);
  t.assert_eq(v1.name, "B");
  t.assert_eq(v1.fields.len(), 1);
  t.assert(ty_is0(v1.fields.get(0), "String"));

  let v2: ast.EnumVariantDecl = e.variants.get(2);
  t.assert_eq(v2.name, "None");
  t.assert_eq(v2.fields.len(), 0);
}

fn test_parse_let_mut_with_type_ann_and_int_init() -> () {
  let r: p.ParseResult = p.parse_text("fn main() -> i32 { let mut x: i32 = 1; return x; }");
  t.assert(ok(r.err));

  let prog: ast.Program = r.prog;
  let funcs: Vec[ast.FuncDecl] = prog.funcs;
  let exprs: ast.ExprPool = prog.exprs;
  let f: ast.FuncDecl = funcs.get(0);
  let stmts: Vec[ast.Stmt] = f.body.stmts;
  t.assert_eq(stmts.len(), 2);

  let st0: ast.Stmt = stmts.get(0);
  let ok0: bool = match st0 {
    ast.Stmt.Let(mutable, name, has_ann, ann, init) =>
      mutable && name == "x" && has_ann && ty_is0(ann, "i32") &&
      match ast.expr_pool_get(exprs, init) { ast.ExprNode.Int(v) => v == 1, _ => false },
    _ => false,
  };
  t.assert(ok0);

  let st1: ast.Stmt = stmts.get(1);
  let ok1: bool = match st1 {
    ast.Stmt.Return(has, id) => has && match ast.expr_pool_get(exprs, id) { ast.ExprNode.Ident(s) => s == "x", _ => false },
    _ => false,
  };
  t.assert(ok1);
}

fn test_parse_call_explicit_type_args() -> () {
  let src: String = "fn id[T](x: T) -> T { return x; }\nfn main() -> i32 { return id[i32](1); }";
  let r: p.ParseResult = p.parse_text(src);
  t.assert(ok(r.err));
  let prog: ast.Program = r.prog;
  t.assert_eq(prog.funcs.len(), 2);
  let f: ast.FuncDecl = prog.funcs.get(1);
  let exprs: ast.ExprPool = prog.exprs;
  let st: ast.Stmt = f.body.stmts.get(0);
  let rid: i32 = match st { ast.Stmt.Return(_, id) => id, _ => -1 };
  let ok_call: bool = match ast.expr_pool_get(exprs, rid) {
    ast.ExprNode.Call(callee, targs, args) =>
      match ast.expr_pool_get(exprs, callee) { ast.ExprNode.Ident(s) => s == "id", _ => false } &&
      targs.len() == 1 && ty_is0(targs.get(0), "i32") &&
      args.len() == 1 && match ast.expr_pool_get(exprs, args.get(0)) { ast.ExprNode.Int(v) => v == 1, _ => false },
    _ => false,
  };
  t.assert(ok_call);
}

fn test_parse_member_call_and_add() -> () {
  let r: p.ParseResult = p.parse_text("fn main() -> i32 { return a.b(1 + 2); }");
  t.assert(ok(r.err));

  let prog: ast.Program = r.prog;
  let exprs: ast.ExprPool = prog.exprs;
  let funcs: Vec[ast.FuncDecl] = prog.funcs;
  let f: ast.FuncDecl = funcs.get(0);
  let stmts: Vec[ast.Stmt] = f.body.stmts;
  let st: ast.Stmt = stmts.get(0);
  let rid: i32 = match st { ast.Stmt.Return(_, id) => id, _ => -1 };

  // return <call>
  let ok_call: bool = match ast.expr_pool_get(exprs, rid) {
    ast.ExprNode.Call(callee, targs, args) =>
      targs.len() == 0 && args.len() == 1 &&
      match ast.expr_pool_get(exprs, callee) {
        ast.ExprNode.Member(recv, name) =>
          name == "b" &&
          match ast.expr_pool_get(exprs, recv) { ast.ExprNode.Ident(s) => s == "a", _ => false },
        _ => false,
      } &&
      match ast.expr_pool_get(exprs, args.get(0)) {
        ast.ExprNode.Binary(op, l, r) =>
          match op { ast.BinaryOp.Add => true, _ => false } &&
          match ast.expr_pool_get(exprs, l) { ast.ExprNode.Int(v) => v == 1, _ => false } &&
          match ast.expr_pool_get(exprs, r) { ast.ExprNode.Int(v) => v == 2, _ => false },
        _ => false,
      },
    _ => false,
  };
  t.assert(ok_call);
}

fn test_parse_postfix_after_paren_expr() -> () {
  // `(expr).foo(...)` should parse as a normal postfix member call.
  let r: p.ParseResult = p.parse_text("fn main() -> i32 { return (a).b(); }");
  t.assert(ok(r.err));
}

fn test_parse_field_assign_stmt() -> () {
  let r: p.ParseResult = p.parse_text("struct S { x: i32 }\nfn main() -> i32 { let mut s: S = S { x: 1 }; s.x = 2; return 0; }");
  t.assert(ok(r.err));
}

fn test_parse_struct_literal_expr() -> () {
  let src: String = "struct S { x: i32, y: i32 }\nfn main() -> i32 { let s: S = S { x: 1, y: 2 }; return 0; }";
  let r: p.ParseResult = p.parse_text(src);
  t.assert(ok(r.err));

  let f: ast.FuncDecl = r.prog.funcs.get(0);
  let exprs: ast.ExprPool = r.prog.exprs;
  let st0: ast.Stmt = f.body.stmts.get(0);
  let init: i32 = match st0 { ast.Stmt.Let(_, _, _, _, id) => id, _ => -1 };
  let ok_lit: bool = match ast.expr_pool_get(exprs, init) {
    ast.ExprNode.StructLit(ty, fields) =>
      match ast.expr_pool_get(exprs, ty) { ast.ExprNode.Ident(s) => s == "S", _ => false } &&
      fields.len() == 2 &&
      fields.get(0).name == "x" &&
      match ast.expr_pool_get(exprs, fields.get(0).expr) { ast.ExprNode.Int(v) => v == 1, _ => false } &&
      fields.get(1).name == "y" &&
      match ast.expr_pool_get(exprs, fields.get(1).expr) { ast.ExprNode.Int(v) => v == 2, _ => false },
    _ => false,
  };
  t.assert(ok_lit);
}

fn test_parse_type_alias_decl() -> () {
  let src: String = "type I = i32\ntype V = Vec[I]\nfn main() -> i32 { let x: I = 1; let mut v: V = Vec(); v.push(x); return v.len(); }";
  let r: p.ParseResult = p.parse_text(src);
  t.assert(ok(r.err));
  t.assert_eq(r.prog.funcs.len(), 1);
  // Type aliases are covered more directly in typecheck tests; parser smoke is enough here.
}

fn test_parse_union_type_decl_smoke() -> () {
  let src: String = "type Value = I32: i32 | Str: String\nfn main() -> i32 { let x: Value = .I32(1); return match x { .I32(v) => v, .Str(_s) => 0 }; }";
  let r: p.ParseResult = p.parse_text(src);
  t.assert(ok(r.err));
  t.assert_eq(r.prog.enums.len(), 1);
  t.assert_eq(r.prog.enums.get(0).name, "Value");
  t.assert_eq(r.prog.enums.get(0).variants.len(), 2);
  t.assert_eq(r.prog.enums.get(0).variants.get(0).name, "I32");
  t.assert_eq(r.prog.enums.get(0).variants.get(1).name, "Str");
}

fn test_parse_const_decl_smoke() -> () {
  let src: String = "const N: i32 = 1 + 2\npub const M: i64 = 3\nfn main() -> i32 { return N; }";
  let r: p.ParseResult = p.parse_text(src);
  t.assert(ok(r.err));
  t.assert_eq(r.prog.consts.len(), 2);
  t.assert_eq(r.prog.consts.get(0).name, "N");
  t.assert_eq(r.prog.consts.get(1).name, "M");
  t.assert(r.prog.consts.get(1).is_pub);
}

fn test_parse_match_expr_enum_variant_pattern() -> () {
  let src: String = "enum E { A(i32), None }\nfn main() -> i32 { let x: E = E.None; return match x { E.A(v) => v, E.None => 0, _ => 1 }; }";
  let r: p.ParseResult = p.parse_text(src);
  t.assert(ok(r.err));

  let f: ast.FuncDecl = r.prog.funcs.get(0);
  let exprs: ast.ExprPool = r.prog.exprs;
  let st1: ast.Stmt = f.body.stmts.get(1);
  let rid: i32 = match st1 { ast.Stmt.Return(_, id) => id, _ => -1 };
  let ok_match: bool = match ast.expr_pool_get(exprs, rid) {
    ast.ExprNode.Match(scr, arms) =>
      match ast.expr_pool_get(exprs, scr) { ast.ExprNode.Ident(s) => s == "x", _ => false } &&
      arms.len() == 3 &&
      match arms.get(0).pat { ast.Pat.EnumVariant(enum_parts, vname, binders) => enum_parts.len() == 1 && enum_parts.get(0) == "E" && vname == "A" && binders.len() == 1 && binders.get(0) == "v", _ => false } &&
      match arms.get(1).pat { ast.Pat.EnumVariant(enum_parts, vname, binders) => enum_parts.len() == 1 && enum_parts.get(0) == "E" && vname == "None" && binders.len() == 0, _ => false } &&
      match arms.get(2).pat { ast.Pat.Wild => true, _ => false },
    _ => false,
  };
  t.assert(ok_match);
}

fn test_parse_match_expr_enum_variant_pattern_shorthand() -> () {
  let src: String = "enum E { A(i32), None }\nfn main() -> i32 { let x: E = .None; return match x { .A(v) => v, .None => 0, _ => 1 }; }";
  let r: p.ParseResult = p.parse_text(src);
  t.assert(ok(r.err));

  let f: ast.FuncDecl = r.prog.funcs.get(0);
  let exprs: ast.ExprPool = r.prog.exprs;
  let st0: ast.Stmt = f.body.stmts.get(0);
  let init: i32 = match st0 { ast.Stmt.Let(_, _, _, _, id) => id, _ => -1 };
  t.assert(match ast.expr_pool_get(exprs, init) { ast.ExprNode.DotIdent(s) => s == "None", _ => false });

  let st1: ast.Stmt = f.body.stmts.get(1);
  let rid: i32 = match st1 { ast.Stmt.Return(_, id) => id, _ => -1 };
  let ok_match: bool = match ast.expr_pool_get(exprs, rid) {
    ast.ExprNode.Match(_scr, arms) =>
      arms.len() == 3 &&
      match arms.get(0).pat { ast.Pat.EnumVariant(enum_parts, vname, binders) => enum_parts.len() == 0 && vname == "A" && binders.len() == 1 && binders.get(0) == "v", _ => false } &&
      match arms.get(1).pat { ast.Pat.EnumVariant(enum_parts, vname, binders) => enum_parts.len() == 0 && vname == "None" && binders.len() == 0, _ => false } &&
      match arms.get(2).pat { ast.Pat.Wild => true, _ => false },
    _ => false,
  };
  t.assert(ok_match);
}

fn test_parse_if_while_assign_and_expr_stmt() -> () {
  // Stage0 does not support String concatenation with `+`, so keep it as a single literal.
  let src: String = "fn main(x: i32) -> i32 {\n  let mut y: i32 = 0;\n  if x < 3 { y = 1; } else { y = 2; }\n  while y < 10 { y = y + 1; }\n  print(\"ok\");\n  return y;\n}";
  let r: p.ParseResult = p.parse_text(src);
  t.assert(ok(r.err));

  let f: ast.FuncDecl = r.prog.funcs.get(0);
  t.assert_eq(f.params.len(), 1);
  t.assert_eq(f.body.stmts.len(), 5);

  t.assert(match f.body.stmts.get(2) { ast.Stmt.While(_, _) => true, _ => false });
  t.assert(match f.body.stmts.get(3) { ast.Stmt.Expr(_) => true, _ => false });
  t.assert(match f.body.stmts.get(4) { ast.Stmt.Return(_, _) => true, _ => false });
}

fn test_parse_if_expr() -> () {
  // if-expression: `if cond { expr } else { expr }`
  let src: String = "fn main(x: i32) -> i32 {\n  let y: i32 = if x < 3 { 1 } else { 2 };\n  return if y < 0 { 0 } else { y };\n}";
  let r: p.ParseResult = p.parse_text(src);
  t.assert(ok(r.err));
}

fn test_parse_if_expr_branches_are_block_exprs() -> () {
  let src: String = "fn main(x: i32) -> i32 {\n  let y: i32 = if x < 3 { let z: i32 = 1; z } else { 2 };\n  return y;\n}";
  let r: p.ParseResult = p.parse_text(src);
  t.assert(ok(r.err));

  let f: ast.FuncDecl = r.prog.funcs.get(0);
  t.assert_eq(f.body.stmts.len(), 2);
  let st0: ast.Stmt = f.body.stmts.get(0);
  let init: i32 = match st0 { ast.Stmt.Let(_m, _n, _ha, _ann, id) => id, _ => -1 };
  let exprs: ast.ExprPool = r.prog.exprs;

  let ok0: bool = match ast.expr_pool_get(exprs, init) {
    ast.ExprNode.If(_c, t_id, e_id) =>
      match ast.expr_pool_get(exprs, t_id) {
        ast.ExprNode.Block(b) =>
          b.stmts.len() == 1 &&
          match b.stmts.get(0) {
            ast.Stmt.Let(_m, name, _ha, _ann, init2) =>
              name == "z" && match ast.expr_pool_get(exprs, init2) { ast.ExprNode.Int(v) => v == 1, _ => false },
            _ => false,
          } &&
          b.has_tail && match ast.expr_pool_get(exprs, b.tail) { ast.ExprNode.Ident(s) => s == "z", _ => false },
        _ => false,
      } &&
      match ast.expr_pool_get(exprs, e_id) {
        ast.ExprNode.Block(b2) =>
          b2.stmts.len() == 0 && b2.has_tail && match ast.expr_pool_get(exprs, b2.tail) { ast.ExprNode.Int(v) => v == 2, _ => false },
        _ => false,
      },
    _ => false,
  };
  t.assert(ok0);
}

fn test_parse_match_arm_block_expr() -> () {
  let src: String = "fn main(x: i32) -> i32 { return match x { 0 => { let y: i32 = 1; y }, _ => { 2 } }; }";
  let r: p.ParseResult = p.parse_text(src);
  t.assert(ok(r.err));

  let f: ast.FuncDecl = r.prog.funcs.get(0);
  t.assert_eq(f.body.stmts.len(), 1);
  let st0: ast.Stmt = f.body.stmts.get(0);
  let rid: i32 = match st0 { ast.Stmt.Return(_has, id) => id, _ => -1 };
  let exprs: ast.ExprPool = r.prog.exprs;

  let ok0: bool = match ast.expr_pool_get(exprs, rid) {
    ast.ExprNode.Match(_scr, arms) =>
      {
        let mut ok: bool = arms.len() == 2;
        if ok {
          let a0: ast.MatchArm = arms.get(0);
          let a1: ast.MatchArm = arms.get(1);
          let ok0: bool = match ast.expr_pool_get(exprs, a0.expr) {
            ast.ExprNode.Block(b) =>
              b.stmts.len() == 1 &&
              match b.stmts.get(0) {
                ast.Stmt.Let(_m, name, _ha, _ann, init2) =>
                  name == "y" && match ast.expr_pool_get(exprs, init2) { ast.ExprNode.Int(v) => v == 1, _ => false },
                _ => false,
              } &&
              b.has_tail && match ast.expr_pool_get(exprs, b.tail) { ast.ExprNode.Ident(s) => s == "y", _ => false },
            _ => false,
          };
          let ok1: bool = match ast.expr_pool_get(exprs, a1.expr) {
            ast.ExprNode.Block(b2) =>
              b2.stmts.len() == 0 && b2.has_tail && match ast.expr_pool_get(exprs, b2.tail) { ast.ExprNode.Int(v) => v == 2, _ => false },
            _ => false,
          };
          ok = ok0 && ok1;
        }
        ok
      },
    _ => false,
  };
  t.assert(ok0);
}

fn test_parse_reports_lex_error() -> () {
  let r: p.ParseResult = p.parse_text("@");
  t.assert(match r.err { p.ParseError.Lex(_) => true, _ => false });
}

fn contains(hay: String, needle: String) -> bool {
  if needle.len() == 0 { return true; }
  if hay.len() < needle.len() { return false; }
  let mut i: i32 = 0;
  while i <= hay.len() - needle.len() {
    if hay.slice(i, i + needle.len()) == needle { return true; }
    i = i + 1;
  }
  return false;
}

fn test_parse_error_format_includes_line_col_unexpected_token() -> () {
  let file: String = "src/main.vox";
  // Parse a valid item, then hit "else" at line 2, col 1.
  let src: String = "fn main() -> i32 { return 0; }\nelse";
  let r: p.ParseResult = p.parse_text_with_path(file, src);
  t.assert(r.err != p.ParseError.None);
  let msg: String = p.parse_error_to_string_with_source(file, src, r.err);
  t.assert(contains(msg, "src/main.vox:2:1"));
  t.assert(contains(msg, "expected fn"));
}

fn test_parse_error_format_includes_line_col_lex_unexpected_char() -> () {
  let file: String = "src/main.vox";
  // '@' at line 2, col 3 (two spaces indentation).
  let src: String = "fn main() -> i32 {\n  @\n  return 0;\n}";
  let r: p.ParseResult = p.parse_text_with_path(file, src);
  t.assert(r.err != p.ParseError.None);
  let msg: String = p.parse_error_to_string_with_source(file, src, r.err);
  t.assert(contains(msg, "src/main.vox:2:3"));
  t.assert(contains(msg, "lex error"));
}
