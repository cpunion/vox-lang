// Codec for const struct values.
//
// We keep ConstSig shape stable by storing struct field payload in `sv`:
//   "N#kind|ty|iv|bv|sv#..."
// where `sv` of each field uses backslash escaping for `\`, `#`, `|`.

pub struct ConstStructDecode {
  pub ok: bool,
  pub fields: Vec[ConstEnumPayloadField],
}

fn empty_struct_decode() -> ConstStructDecode {
  return ConstStructDecode { ok: false, fields: Vec() };
}

pub fn const_struct_pack(fields: Vec[ConstEnumPayloadField]) -> String {
  let mut out: String = fields.len().to_string();
  let mut i: i32 = 0;
  while i < fields.len() {
    let f: ConstEnumPayloadField = fields.get(i);
    let btxt: String = if f.bv { "1" } else { "0" };
    let ent: String =
      f.kind.to_string()
        .concat("|").concat(f.ty.to_string())
        .concat("|").concat(f.iv.to_string())
        .concat("|").concat(btxt)
        .concat("|").concat(escape_enum_part(f.sv));
    out = out.concat("#").concat(ent);
    i = i + 1;
  }
  return out;
}

pub fn const_struct_unpack(s: String) -> ConstStructDecode {
  let parts: Vec[String] = split_escaped(s, 35); // '#'
  if parts.len() == 0 { return empty_struct_decode(); }

  let npr: ParseI64DecResult = parse_i64_dec(parts.get(0));
  if !npr.ok || npr.val < 0 { return empty_struct_decode(); }
  let n: i32 = npr.val as i32;
  if parts.len() != n + 1 { return empty_struct_decode(); }

  let mut fs: Vec[ConstEnumPayloadField] = Vec();
  let mut i: i32 = 0;
  while i < n {
    let ep: Vec[String] = split_escaped(parts.get(i + 1), 124); // '|'
    if ep.len() != 5 { return empty_struct_decode(); }

    let kpr: ParseI64DecResult = parse_i64_dec(ep.get(0));
    let tpr: ParseI64DecResult = parse_i64_dec(ep.get(1));
    let ipr: ParseI64DecResult = parse_i64_dec(ep.get(2));
    if !kpr.ok || !tpr.ok || !ipr.ok { return empty_struct_decode(); }

    let btxt: String = ep.get(3);
    if btxt != "0" && btxt != "1" { return empty_struct_decode(); }
    let bv: bool = btxt == "1";

    fs.push(ConstEnumPayloadField {
      kind: kpr.val as i32,
      ty: tpr.val as i32,
      iv: ipr.val,
      bv: bv,
      sv: ep.get(4),
    });
    i = i + 1;
  }

  return ConstStructDecode { ok: true, fields: fs };
}
